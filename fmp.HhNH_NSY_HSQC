;HHC

;HH NOESY with INEPT xfer H detect
;written by WTF
;Avance II+ version

;######################################################
;#                                                    #
;# H-N INEPT Transfer Experiment, H detect            #
;#                                                    #
;######################################################

;$COMMENT= HHN INEPT
;$CLASS=Solids
;$DIM=3D
;$TYPE= 1H detect Hetero
;$SUBTYPE=NOESY HSQC
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <HCN_defs.incl>

;######################################################
;#          Define Scalar Mixing Parameters           #
;######################################################

define delay TauHN
"TauHN=d26"

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-p3*2/3.14-0.3u"   ;#   t1_init => 90,-180   #
"in30=inf1"                 ;##########################

"in10=inf2"                 ;##########################
"d10=in10/2-p3*2/3.14"      ;#   t2_init => 90,-180   #
"in29=inf2"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;#                        #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;#                        #
define loopcounter T2evo    ;# Power Deposition Calcs #
"T2evo=larger(td2,cnst29)"  ;#                        #
"d29=T2evo*in29"            ;##########################

define loopcounter go_t1
"go_t1=td1"
define loopcounter go_t2
"go_t2=td2"

aqseq 312

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

"d30=d30/2"                   ;# divide by 2 for mc macro #
"d29=d29/2"                   ;# divide by 2 for mc macro #

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#ifdef Pre
"ONTIME=aq+4*p3+2*p4+5*p1+4*p2+4*p21+2*p22+d18"
#endif

#ifndef Pre
"ONTIME=aq+4*p3+2*p4+5*p1+4*p2+4*p21+2*p22"
#endif

#include <1H_ON_prot.incl>
; Protections for Proton Presat, H Acq, etc

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

;######################
;#      PRESAT        #
;######################

#ifdef Pre
                       3u pl18:H
                       d18 cw:H ph20
                       d13 do:H
#endif

                        (p3  pl2  ph1):H

;########################
;#    t1 evolution      #
;########################

0.3u

if "go_t1>1"
{
  (center
    (d0):H
    (p1 pl1 ph20 p2 ph21 p1 ph20):C
    (p21 pl21 ph20 p22 ph21 p21 ph20):N
  )
}

;###################
;#   HH NOESY      #
;###################

                         (p3  pl2  ph2):H

  TauR
  d8
  TauR

                         (p3  ph3):H
  TauHN
  (center (p22 pl21 ph4):N (p4 ph3):H )
  TauHN

;#############
;# 1H Purge  #
;#############

#ifdef PP
  (p5 ph3):H
#endif

;##############
;# Store IzSz #
;##############

                         (p3 ph5):H

;##########################
;# Time evolution on IzSx #
;##########################

  d2
  (p21 ph6):N

if "go_t2>1"  
{
  (center
    (p3 pl2 ph20 p4 ph21 p3 ph20):H
    (p1 pl1 ph20 p2 ph21 p1 ph20):C
    (d10):N
  )
}
if "go_t2<=1"  
{
    (p3 pl2 ph20 p4 ph21 p3 ph20):H
}

  (p21 ph7):N
  d2

  (p3 ph8):H
  TauHN
  (center (p22 pl1 ph3):N (p4 ph3):H )
  TauHN

  0.5u pl29:N     ; GARP power on 15N
  1u  cpd1:N

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	           	;start ADC with ph31 signal routing

1m do:N 			;decoupler off

lo to Start times ns

30m mc #0 to Start F1PH(ip1,id0 & dd30) 
                  F2PH(rd0 & rp1 & dp7, id10 & dd2 & rd30)

HaltAcqu, 1m
exit
  
;ph1 = {1}*4 {3}*4      ; H exc
;ph2 = {3}*8 {1}*8      ; H store
;ph3 = 1                ; H exc 
;ph4 = 0                ; X echo
;ph5 = 0                ; H store
;ph6 = 1 3              ; N exc
;ph7 = 3 3 1 1          ; N store
;ph8 = 1 2              ; H exc

ph1= 0 2
ph2= 2
ph3= 0
ph4= {0}*16 {2}*16
ph5= 1
ph6= {0}*2  {2}*2
ph7= {0}*4  {2}*4
ph8= {0}*8  {2}*8
ph9= {0}*16 {2}*16

ph20= 0
ph21= 1
ph22= 2

ph31= 2 0 0 2 0 2 2 0   0 2 2 0 2 0 0 2

;##########################################################

;PARAMETERS:
;p1      : C 90 pl1
;p2      : C 180 pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;d1      : recycle delay
;d4      : HC INEPT time (1/4J ~ 1.5ms)
;d5      : HC INEPT time (1/4J ~ 1.5ms)
;d26     : HN INEPT time (1/4J ~ 2.5ms)
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
