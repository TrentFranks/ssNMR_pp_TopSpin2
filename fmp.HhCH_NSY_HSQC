;HHC

;HH NOESY with INEPT xfer H detect
;written by WTF
;Avance II+ version

;######################################################
;#                                                    #
;# H-C INEPT Transfer Experiment, C detect            #
;#                                                    #
;######################################################

;$COMMENT= HHC INEPT
;$CLASS=Solids
;$DIM=3D
;$TYPE= 1H detect Hetero
;$SUBTYPE=NOESY HSQC
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <HCN_defs.incl>


;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-p3*2/3.14-0.3u"   ;#   t1_init => 90,-180   #
"in30=inf1"                 ;##########################

"in10=inf2"                 ;##########################
"d10=in10/2-p3*2/3.14"      ;#   t2_init => 90,-180   #
"in29=inf2"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;#                        #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;#                        #
define loopcounter T2evo    ;# Power Deposition Calcs #
"T2evo=larger(td2,cnst29)"  ;#                        #
"d29=T2evo*in29"            ;##########################

define loopcounter go_t1
"go_t1=td1"
define loopcounter go_t2
"go_t2=td2"

aqseq 312

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#ifdef Pre
"ONTIME=aq+4*p3+2*p4+5*p1+4*p2+4*p21+2*p22+d18"
#endif

#ifndef Pre
"ONTIME=aq+4*p3+2*p4+5*p1+4*p2+4*p21+2*p22"
#endif

#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

;######################
;#      PRESAT        #
;######################

#ifdef Pre
                       3u pl18:H
                       d18 cw:H ph20
                       d13 do:H
#endif

                        (p3  pl2  ph1):H

;########################
;#    t1 evolution      #
;########################

0.3u

if "go_t1>1"
{
  (center
    (d0):H
    (p1 pl1 ph20 p2 ph21 p1 ph20):C
    (p21 pl21 ph20 p22 ph21 p21 ph20):N
  )
}

;###################
;#   HH NOESY      #
;###################

                         (p3  pl2  ph2):H

  TauR
  d8
  TauR
                         (p3  pl2  ph3):H

  d4
  (center (p2 pl1 ph4):C (p4 ph3):H )
  d4

;#############
;# 1H Purge  #
;#############

#ifdef PP
  (p5 ph3):H
#endif

;##############
;# Store IzSz #
;##############

  (p3 ph5):H

;##########################
;# Time evolution on IzSx #
;##########################

  d2
  (p1 ph6):C

if "go_t2>1"  
{
  (center
    (p3 pl2 ph20 p4 ph21 p3 ph20):H
    (d10):C
    (p21 pl21 ph20 p22 ph21 p21 ph20):N
  )
}
  (p1 ph7):C
  d2

  (p3 ph8):H
  d5
  (center (p2 pl1 ph3):C (p4 ph3):H )
  d5

  0.3u

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	           	;start ADC with ph31 signal routing

;1m do:H 			;decoupler off

lo to Start times ns

30m mc #0 to Start F1PH(ip1,id0 & dd30) 
                  F2PH(rd0 & rp1 & ip7, id10 & dd2 & rd30)

HaltAcqu, 1m
exit
  
ph1 = {1}*4 {3}*4      ; H exc
ph2 = {3}*8 {1}*8      ; H store
ph3 = 1                ; H exc 
ph4 = 0                ; X echo
ph5 = 0                ; H store
ph6 = 1 3              ; C exc
ph7 = 3 3 1 1          ; C store
ph8 = 1 2              ; H exc

ph20= 0
ph21= 1

ph31= 0 3 2 1   2 1 0 3
      2 1 0 3   0 3 2 1  ; receiver

;##########################################################

;PARAMETERS:
;p1      : C 90 pl1
;p2      : C 180 pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;d1      : recycle delay
;d4      : HC INEPT time (1/4J ~ 1.5ms)
;d5      : HC INEPT time (1/4J ~ 1.5ms)
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
