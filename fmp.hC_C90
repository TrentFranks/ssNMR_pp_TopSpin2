;hX_X90

;######################################################
;#                                                    #
;# H-C CP with ping                                   #
;#                                                    #
;# p1 is Zero Cross (C pi/2)                          #
;#                                                    #
;######################################################


;Cross Polarization for X channel (13C)with X90
;written by WTF
;Avance II+ version

;$COMMENT= H-X CP with additional X90
;$CLASS=Solids
;$DIM=1D
;$TYPE=CPMAS
;$SUBTYPE=Setup
;$OWNER=Guest
prosol relations=<solids_cp>

#include <Avancesolids.incl>
#include <C_detect_def.incl>

define delay ONTIME
;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p10"

#include <Standard_Prot.incl>
;      acquisition, CP time, PDSD/DARR, and echo restrictions
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H
                         
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H
  0.3u cpds2:H ;decouple 1H during acquisition

  (p1  pl1  ph3):C

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31         
  1m do:H
  d1 mc #0 to Start F0(zd)
  
HaltAcqu, 1m
exit

ph0 = 0                ; H CP Spin lock
ph1 = 1 3              ; H Hard Pulse
ph2 = 0 0 2 2 1 1 3 3  ; X CP Spin Lock 
ph3 = 1 1 3 3 2 2 0 0  ; X90

ph31= 0 2 2 0 1 3 3 1  ; receiver

;######################################################

;PARAMETERS:
;p1      : C 90 (pl1)  use -Dp90 
;p3      : H 90 at pl2
;p10     : H-C CP at pl10 (C) and sp0 (H)
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl10    : X CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl20    : H CP pwr
;d1      : recycle delay
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
