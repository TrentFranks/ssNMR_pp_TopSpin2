;hC

;Cross Polarization for X channel (13C)
;written by WTF
;Avance II+ version

;######################################################
;#                                                    #
;# H-C Cross Polarization Experiment                  #
;#                                                    #
;#  Adjust pl10, pl20, and p10 for maximum signal.    #
;#  The Hartman-Hahn CP condidition is B1(H)=B1(C)+-1 #
;#  sp0 is usually a linear or tangent ramp           #
;#  p10 is usually 1 ms                               #
;#                                                    #
;######################################################

;$COMMENT= HC with Phase Inversion CP
;$CLASS=Solids
;$DIM=1D
;$TYPE=CPMAS
;$SUBTYPE=Setup
;$OWNER=guest

;prosol relations=<solids_cp>

define delay ONTIME
;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p10+p20"

#include <Standard_Prot.incl>
        ;acq, CP time, etc.
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

                         (p3  pl2  ph1):H
  1u
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H
  (p20:spf10 pl10 ph2):C  (p20 pl20 ph10):H

  1u cpds2:H ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31         
  1m do:H
  d1 mc #0 to Start F0(zd)
  
HaltAcqu, 1m
exit

ph0 = 0                ; H CP Spin lock
ph1 = 1 3              ; H Hard Pulse
ph2 = 0 0 2 2 1 1 3 3  ; X CP Spin Lock 
ph10= 2
ph31= 0 2 2 0 1 3 3 1  ; receiver

;#########################################################################

;PARAMETERS:
;p1      : C 90 (pl1)  use -Dp90 
;p2      : C 180 (pl1) use -Decho
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;p10     : H-C CP at pl10 (f1,X) and sp0 (f2,H)
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl10    : X CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl20    : H CP pwr
;d1      : recycle delay
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam8  : soft X 180 e.g. rSnob
