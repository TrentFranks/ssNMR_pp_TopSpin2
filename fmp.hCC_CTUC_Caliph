;CC PDSD/DARR experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#  CTUC COSY (CA-CO opt)                             #
;#  Chen, L.; Olsen, R.; Elliot, D; Boettcher, J;     #
;#    Zhou, D; Rienstra, C; and Mueller, L.           #
;#    J Am Chem Soc 2006, 128, 9992-9993.             #
;#                                                    #
;#  ns = n*4. 16 is full cycle                        #
;#                                                    #
;######################################################
;
; Options DP, Cpre
; written W Trent Franks

;$COMMENT= HC CP with C COSY mixing
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=ZQ mixing
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Z-filter                  #
;######################################################

#include <fmp.DARR.subr>

define delay TauCC
define delay TauCCmSP
"TauCC=d21"
"TauCCmSP=d21-p8"

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

"in0 =inf1/2"               ;##########################
"in30=inf1/2"               ;#                        #
"d0  =d21-0.3u-p8/2"        ;#     t1_init => 0,0     #
"d30 =0.3u"                 ;#   Definitions for CT   #
"d22 =d21-p8/2-p9"          ;#                        #
define delay ONTIME         ;##########################

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################
Prepare, ze

"ONTIME=aq+d0+d30+p10+2*d21";

;######################################################
;#               Protections: Pre-Check               #
;###############################88#######################

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <DARR_prot.incl>
        ;Z-filter protections
#include <t1_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;###############################8#######################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row
#include <t1evol_prot.incl>

#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;##############################8#######################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C

#else
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H
#endif

0.3u cpds2:H

d0                          ;##########################
(p8:spf8 pl28 ph3):C        ;#   Scalar and t1 evol   #
d22                         ;# adjusted Tau(C-C) for  # 
(p9:spf9 pl29 ph9):C        ;#      soft pulses       #
d30                         ;##########################

(p1 pl1 ph4):C              ;##########################
10u                         ;#   DQ,ZQ filter step    #
(p1 pl1 ph5):C              ;##########################

TauCCmSP                    ;##########################
(p8:spf8 pl28 ph6):C        ;#  Scalar evol refocus   #
TauCCmSP                    ;##########################

0.3u do:H

;######################################################
;#              Z-filter step using DARR              #
;######################################################

  (p1 pl1 ph7):C   (p1 pl2):H  ;store

  subr PDSD_DARR()

  (p1 pl1 ph8):C   (p1 pl2):H  ;readout

  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;###############################8########################

gosc ph31 	           	;start ADC with ph31 signal routing

1m do:H 				;decoupler off
lo to Start times ns

d1 mc #0 to Start F1PH(ip2,dd0 & id30 & ip10) 

HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=16)            #
;#######################################################

ph0=  0
ph1=  1
ph2=  0 2
ph3=  0 2 1 3 2 0 3 1
ph4=  1 3
ph5=  0
ph6=  {1}*8 {2}*8 {3}*8 {0}*8
ph7=  0
ph8=  2
ph9=  0 1

ph10= 1 3  ; X90 DP

ph20= 0 2

ph31= 0 0 2 2  0 0 2 2   2 2 0 0  2 2 0 0

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C Presat (-DCpre)
;p6      : C soft 90  (pl26)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;sp0     : H CP pwr
;pl1     : C HP pwr
;pl2     : H HP pwr8
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl28    : C soft 90 pwr
;pl29    : C soft 90 pwr
;d1      : recycle delay
;d8      : PDSD/DARR mix time
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam8  : C soft 180 on-res refocus pulse (e.g. rSNOB)
;spnam9  : C soft 180 off-res refocus pulse (e.g. rSNOB)
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) DP, CPRE
