;CC PDSD/DARR experiment (TopSpin 2.3)
;
;######################################################
;#                                                    #
;#  TOBSY  fill in later                         #
;#                                                    #
;#  ns = n*4. 16 is full cycle                        #
;#                                                    #
;#  Indirect Dimension phasing is 90,-180             #
;#                                                    #
;######################################################
;
; Options DP, T961, Cpre, CDC
; written W Trent Franks, Monica Santos de Freitas

;$COMMENT= HC CP with C Scalar mixing
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=ZQ mixing
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Mixing                    #
;#     TOBSY 9_3^1 default: -DT961 for tobsy 9_6^1    #
;######################################################

#include <fmp.TOBSY.subr>

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-0.3u-p1*2/3.14159";#   t1_init => 90,-180   #
"in30=inf1"                 ;#                        #
define loopcounter go_t1    ;#   No increment if 1D   #
"go_t1=td1"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;##########################
                             
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

"d30=d30/2"                 ;#divide by 2 for mc macro#

#ifdef T961
  "ONTIME=aq+d0+d30+p10+p8+2*C961mix"
#else
  "ONTIME=aq+d0+d30+p10+p8+2*C931mix"
#endif

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <TOBSY_prot.incl>
            ;loop l3 < 100; RFDRmix < 10ms
#include <t1_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

                            ;##########################  
  1m rpp28                  ;#  C5 phases reset here  #
  1m rpp29                  ;##########################

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row                            
#include <t1evol_prot.incl> 
                            
#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;######################################################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C 
#else
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H

#endif

if "go_t1>1"
{                           ;##########################
  d0 cpds2:H                ;#      t1 evolution      #
  0.3u do:H                 ;##########################
}

;######################################################
;#              Homonuclear TOBSY Mixing              #
;######################################################
  
  (p1 pl1 ph3):C   (p1 pl2):H  ;store

#ifdef T961            
  subr C961_TOBSY()
#else
  subr C931_TOBSY()
#endif
  
  (p1 pl1 ph4):C   (p1 pl2):H  ;readout
    
  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	           	;start ADC with ph31 signal routing

#ifdef CDC
  d30                   ; Constant duty cycle
#endif

1m do:H 				;decoupler off
lo to Start times ns

d1 mc #0 to Start F1PH(dp3,id0 & dd30) 

HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=16)            #
;#######################################################

ph0=  0
ph1=  1 3
ph2=  {0}*8 {2}*8
ph3=  1 1 3 3  
ph4=  1 1 2 2  3 3 0 0  

ph10= 1 3 1 3  1 3 1 3   
      3 1 3 1  3 1 3 1  ; X90 DP

ph20= 0 2

;#######################################################
;#                   TOBSY Phases                      #
;#######################################################

ph29=(360)   0  40  80 120 160 200 240 280 320
ph28=(360) 180 220 260 300 340 380 420 460 500 

ph31= 2 0 1 3   0 2 3 1
      0 2 3 1   2 0 1 3

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C Presat (-DCpre)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;sp0     : H CP pwr
;pl1     : C HP pwr
;pl2     : H HP pwr
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl19    : TOBSY C pwr
;C931mix : TOBSY 931 mix time
;pC93190 : 931 p90
;C961mix : TOBSY 961 mix time
;pC96190 : 961 p90
;d1      : recycle delay
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam6  : C soft 90 purge pulse (e.g. eSNOB)
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) D1, DP, CDC, Cpre, T961
