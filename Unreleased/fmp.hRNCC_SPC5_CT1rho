; hNCC3D with SPC5 mixing
; MINIMUM phase cycle= 4
;
;######################################################
;#                                                    #
;# N-C SPECIFIC CP                                    #
;# Baldus, M.; Petkova, A. T.; Herzfeld, J. H.;       #
;#    Griffin, R. G. Mol. Phys. 1998,95, 1197-1207.   #
;# M. Hong and R.G. Griffin;                          #
;#    J. Am. Chem. Soc. 120, 7113-7114                #
;#                                                    #
;#  pl7 (15N) = 5*wr/2                                #
;#  pl6 (13C) = 3wr/2(NCA) 7wr/2(NCO)                 #
;#                                                    #
;# N-C PAIN CP (use square pulse shape)               #
;#  J. Lewandowski, G. De Pape, and                   #
;#     R. G. Griffin; J Am Chem Soc. 129(4): 728-29   #
;#                                                    #
;#  pl7 (15N) = 5*wr/2                                #
;#  pl6 (13C) = 5*wr/2   and various other conditions #
;#                                                    #
;# SPC5(_2,_3) DQ mixing                              #
;#  SPC5_2  (Broadband)                               #
;#   M. Hohwy, C.M. Rienstra, C.P. Jaroniec, and      #
;#     R.G. Griffin J. Chem. Phys. 1999,110,7983-7992 #
;# SPC5_3  (Narrowband)                               #
;#   M. Hohwy, C. M. Rienstra, R. G. Griffin          #
;#     J. Chem. Phys. 2002, 117 4973-4987             #
;#                                                    #
;#  pl15(SPC5_3)= 10/3*wr; l5 >= n*10 ; ns = n*4      #
;#                                                    #
;#  pl15(SPC5_2)= 5*wr; l5 >= n*10 ; ns = n*4         #
;#                                                    #
;######################################################
;
; HN-CP, then NC-CP, then CC mixing
; written by W Trent Franks
; FMP Berlin
;
;$COMMENT=hNCC Double CP 
;$CLASS=Solids
;$DIM=3D
;$TYPE=Heteronuclear
;$SUBTYPE=Relaxation
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Mixing                    #
;#    SPC5_3 is Default; SPC5_2 mixing with -DSPC52   #
;######################################################

#include <fmp.SPC5.subr>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-0.3u-p1*2/3.14159";#   t1_init => 90,-180   #
"in30=inf1"                 ;##########################

"in10=inf2"                 ;##########################
"d10=in10/2-0.3u "          ;#   t2_init => 90,-180   #
"in29=inf2"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;#                        #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;#                        #
define loopcounter T2evo    ;# Power Deposition Calcs #
"T2evo=larger(td2,cnst29)"  ;#                        #
"d29=T2evo*in29"            ;##########################

define delay T1rhoMAX
define delay T1rhoAPPL

"T1rhoMAX=cnst18*1us"
"T1rhoAPPL=larger(1us,T1rhoMAX-p18)"

define loopcounter go_t1
"go_t1=td1"
define loopcounter go_t2
"go_t2=td2"
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################
aqseq 312

Prepare, ze

"d30=d30/2"                 ;#divide by 2 for mc macro#
"d29=d29/2"                 ;#divide by 2 for mc macro#


#ifdef SPC52
  "ONTIME=aq+d0+d30+d10+d29+p11+p16+p8+2*SPC52mix"
#else
  "ONTIME=aq+d0+d30+d10+d29+p11+p16+p8+2*SPC53mix"
#endif

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
       ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <SPC5_prot.incl>
       ;DQ mix parameters
#include <t1_prot.incl>
        ;check t1 evolution time
#include <t2_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

  1m rpp15                  ;##########################
  1m rpp16                  ;#  C5 phases reset here  #
  1m rpp25                  ;##########################
  1m rpp26

;######################################################
;#               Protections: In Situ                 #
;######################################################

#include <t1evol_prot.incl>
            ;check d0 and d30
#include <t2evol_prot.incl>
            ;check d10 and d29

#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;######################################################
;#                Initial excitation                  #
;######################################################

                         (p3  pl2  ph1):H
                         (p11 pl19 ph0):H (p11:spf1 pl11 ph2):N
                   
0.3u

if "go_t2>1"
{                           ;##########################
  0.3u cpds2:H              ;#      t1 evolution      #
                            ;##########################
  (center
    (d10):N 
    (p1 pl1 ph20 p2 ph21 p1 ph20):C 
  )
  0.3u do:H ;

}

;######################################################
;#             Second CP Period (SPECIFIC)            #
;######################################################

 (p16:spf2 pl6 ph4):C (p16 pl13 ph1):H (p16 pl7 ph3):N  

;######################################################
;#               Carbon T1 rho period                 #
;######################################################

  d18

  (p18 pl18 ph5):C  (p18 pl13 ph1):H


if "go_t1>1"  
{                           ;##########################
  0.3u cpds2:H              ;#      t2 evolution      #
                            ;##########################
  (center
    (d0):C 
    (p21 pl21 ph20 p22 ph21 p21 ph20):N
  )
  0.3u do:H 
}

;######################################################
;#           Homonuclear SPC5_(3,2) Mixing            #
;##########################!###########################
  
  (p1 pl1 ph6):C   (p1 pl2):H  ;store

#ifdef SPC52
  subr SPC5_2exc()
  subr SPC5_2rec()
#else
  subr SPC5_3exc()
  subr SPC5_3rec()
#endif
  
  (p1 pl1 ph7):C   (p1 pl2):H  ;readout
    
  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 		;start ADC with ph31 signal routing

#ifdef CDC
  d29                   ; T1 (d10) Nitrogen Const. Duty
  d30                   ; T2 (d0) Carbon Const. Duty
  T1rhoAPPL
#endif

1m do:H 				;decoupler off
lo to Start times ns

30m mc #0 to Start F1PH(dp6, id0 & dd30)
                  F2PH(rd0 & rp5 & ip2, id10 & dd29 & rd30)

HaltAcqu, 1m
exit

ph0 = 0
ph1 = {1}*16 {3}*16
ph2 = {0}*32 {2}*32
ph3 = 0 2 0 2
ph4 = 0 0 2 2
ph5 = 0 0 0 0  2 2 2 2
ph6 = {3}*8  {1}*8
ph7 = 1 1 2 2


ph31= 0 2 3 1  2 0 1 3  2 0 1 3  0 2 3 1
      2 0 1 3  0 2 3 1  0 2 3 1  2 0 1 3
      2 0 1 3  0 2 3 1  0 2 3 1  2 0 1 3
      0 2 3 1  2 0 1 3  2 0 1 3  0 2 3 1

ph20= 0
ph21= 1
ph22= 2

;########
;# SPC5 #
;########

ph15= (360) 90 162 234 306 18 270 342 54 126 198
ph16= (360) 270 342 54 126 198 90 162 234 306 18
ph25= (360) 0 72 144 216 288 180 252 324 36 108
ph26= (360) 180 252 324 36 108 0 72 144 216 288

;####################################################

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p4      : H 180 (pl2)
;p0      : C Presat (-DCPRE)
;p11     : HN CP contact pl11 (N) and sp1 (H)
;p16     : NC CP contact pl7 (N) and pl6 (C) pl13 H
;p21     : N 90 
;p22     : N 180

;pl0     : C PRESAT pwr
;pl1     : C HP pwr
;pl2     : H HP pwr
;pl7     : N NC-CP pwr 
;pl6     : C NC-CP pwr
;pl11    : N HN CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr
;pl15    : C SPC5 pwr (10/3wr if SPC53; 5wr if SPC52)
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl21    : N HP pwr

;l5      : SPC5 repetitions (doubled)
;SPC52mix: SPC52 mix time (half)
;SPC53mix: SPC53 mix time (half)

;pcpd2   : H dec pulse

;spnam1  : HN CP file
;spnam2  : NC CP shape (shape on C)
;sp1     : H CP pwr (HN) (p11)
;o1 	 : CA or CO frequency
;cnst1 	 : o1+cnst1=offset for specific CP
;cnst2 	 : offset for last X pulse
;d1      : recycle delay
;in0 	 : 1/swh 15N t1 dimension
;cpdprg2 : Decoupling file cw, tppm (at pl12)
;cpdprg3 : RFDR Decoupling cw, tppm (at pl13)
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) SPC52, CDC, CPRE
