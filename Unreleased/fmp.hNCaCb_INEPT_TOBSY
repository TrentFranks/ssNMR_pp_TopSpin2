; hNCaCb 3D with INEPT xfers and TOBSY mixing
;
;######################################################
;#                                                    #
;# H-N-C SPECIFIC INEPT                               #
;# Zhong, L.; Bamm, V.; Ahmed, M.; Harauz, G.;        #
;#    Ladizhansky, V.; Biochem. Biophys. Acta, 1768   #
;#    (2007) 3193-3205                                #
;#                                                    #
;######################################################
;
;hNCC 3D with constant time evolution in t1(N15) and t2(CO);
;  using INEPT for HN and NC (Selective) polarization transfer10
;adapted from Ligang Zhong, July 18/2006 by WT Franks Jan 2012

;$COMMENT=hNCC INEPT transfers
;$CLASS=Solids
;$DIM=3D
;$TYPE=Heteronuclear
;$SUBTYPE=hNCC INEPT
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>10
#include <C_detect_def.incl>

;######################################################
;#      Define Scalar Mixing Schemes and timings      #
;# TOBSY 9_3^1 is Default, 9_6^1 & adiabatic w/ optns #
;######################################################

#include <fmp.TOBSY.subr>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

"d27=p9"
"d26=d6-p9/2-larger(p22,p8)/2"
"d30=d6-larger(p22,p8)/2-p1/2"

"d28=d5-p9/2-larger(p22,p8)/2"
"d29=d5-larger(p22,p8)/2-larger(p3,p1)/2"

"in0=inf1/2"                ;##########################
"d0=0.3u"                   ;#     t1_init => 0,0     #
"in28=in0"                  ;#      Nitrogen Dim      #
"in29=in0"                  ;##########################

"in10=inf2/2"               ;##########################
"d10=0.3u"                  ;#     t2_init => 0,0     #
"in26=in10"                 ;#       Carbon Dim       #
"in30=in10"                 ;##########################

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

define delay ONTIME
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################
aqseq 312

Prepare, ze 

#ifdef T961
  "ONTIME=aq+2*(d5+d6)+C961mix"
#else
  "ONTIME=aq+2*(d5+d6)+C931mix"
#endif

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
       ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <t1_prot.incl>
        ;check t1 evolution time
#include <t2_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

#include <t1evol_prot.incl>
            ;check d0 and d30
#include <t2evol_prot.incl>
            ;check d10 and d29

#ifdef Cpre
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

                            ;##########################  
  1m rpp28                  ;#  C9 phases reset here  #
  1m rpp29                  ;##########################

;######################################################
;#            Initial HN INEPT excitation             #
;######################################################

  (p3 pl2 ph1):H
   d4
  (center (p4 ph2):H (p22 pl21 ph4):N)
   d4
  (lalign (p3 ph3):H (p21 ph5):N )
   d3                              ;###################
  (center (p4 ph2):H (p22 ph6):N ) ;# Refocused INEPT #
   d3                              ;###################

                            ;##########################
   1u pl14:f2 cpds4:f2      ;#  decouple during NCA   #
                            ;##########################

;############################
;# Constant time adjustment #
;############################
   d27
  (p22 ph6):f3              ;##########################
   d0                       ;#      t1 N15 evol.      #
  (p9:spf9 pl29 ph7):f1     ;#  Dec CO, JCN refocused #
   d0                       ;##########################

;################################
;# NCA Selective INEPT transfer #
;################################
   d28
   (center 
    (p22 ph8):N
    (p8:spf8 pl28 ph9):C
   )
   d29

   (center
    (p21 ph10):N
    (p1 pl1 ph11):C
   )  ; creates CA antiphase
   d27

   (p2 pl1 ph13):C
   d10
   (p9:spf9 ph17):C
   d10
   d26

  (center 
   (p22 pl21 ph12):N 
   (p8:spf8 pl28 ph13):C
  )
  d30 

  0.3u do:H

;######################################################
;#              Homonuclear TOBSY Mixing              #
;######################################################

  (p1 pl1 ph18):C   (p1 pl2):H  ;store

#ifdef T961            
  subr C961_TOBSY()
#else
  subr C931_TOBSY()
#endif

  (p1 pl1 ph19):C   (p1 pl2):H  ;readout

  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31
  1m do:f2 do:f3

 30m mc #0 to Start
    F1PH(ip5 & ip6 & ip8,id0 & dd29 & dd28)
    F2PH(rd0 & rd29 & rd28 & ip18 ,id10 & dd30 & dd26)

HaltAcqu, 1m		;jump address for protection files 
 exit

ph1=0
ph2=0
ph3=1 3
ph4=0
ph5=0
ph6=0
ph7=0
ph8=0
ph9=0
ph10=0
ph11=0
ph12=0
ph13=1
ph17=0
ph18=1
ph19=1 1 2 2 3 3 0 0 
ph28=(360) 180 220 260 300 340 380 420 460 500 
ph29=(360)   0  40  80 120 160 200 240 280 320
ph31=0 2 1 3 2 0 3 1 

;###############
;# PARAMETERS: #
;###############

;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p4      : H 180 (pl2)
;p8      : C soft 180 ON res (pl28)
;p9      : C soft 180 OFF res (pl29)
;p21     : N 90 
;p22     : N 180;cnst31  : MAS Rate
;pl1     : C HP pwr
;pl2     : H HP pwr
;pl12    : H dec pwr;cnst31  : MAS Rate
;pl13    : H alt dec pwr (Spinlock and CW)
;pl14    : H alt dec pwr NC? transfer
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl21    : N HP pwrph28=(360) 180 220 260 300 340 380 420 460 500 

;pl28    : C soft ON res 180 pwr
;pl29    : C soft OFF res 180 pwr
;d0      : evolution increment for N(t1)
;d1      : recycle delay
;d3      : excite HN
;d4      : convert HN
;d5      : excite NC?
;d6      : convert NC?
;d10     : evolution increment for C?(t2)
;d30     : evolution decrement for C?(t2)
;d27     : p9
;d28     : evolution decrement for N(t1)
;d29     : evolution decrement for N(t1)
;spnam8  : C soft 180 ON-res refocus pulse (e.g. RSnob)
;spnam9  : C soft 180 OFF-res refocus pulse (e.g. RSnob)
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;l9      : TOBSY mixing time = ((p9*54.33*4) * l1)
;cnst31  : MAS Rate
