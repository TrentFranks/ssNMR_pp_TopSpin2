;HC INEPT excitation with DARR/PDSD experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#                                                    #
;#  ns = n*4. 16 is full cycle                        #
;#                                                    #
;#  Indirect Dimension phasing is 90,-180             #
;#                                                    #
;######################################################
;
; Options DP, purge, Cpre
; written Barth van Rossum, W Trent Franks

;$COMMENT= HC INEPT with C Z-mixing
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=ZQ mixing
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Mixing                    #
;#  PDSD/DARR is Default, add Selective 90 w/ option  #
;######################################################

#include <fmp.DARR.subr>

define delay t_HC_INEPT
define delay t_CC_INEPT
"t_HC_INEPT=0.25s/145"
"t_HC_INEPT=0.25s/55"

define delay TauCC
define delay TauCCmSP
define delay TauCCmSP2
"TauCC=d21-p8/2-0.3u"
"TauCCmSP=d21-p8/2"
"TauCCmSP2=d21-p8/2-p9"
"d30=0.3u"

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-0.3u-p1*2/3.14159";#   t1_init => 90,-180   #
"in30=inf1"                 ;#                        #
define loopcounter go_t1    ;#   No increment if 1D   #
"go_t1=td1"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;##########################
                             
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################


Prepare, ze

"ONTIME=aq+d0+d30+p8+(4*d21)" ;#  DARR left out   #

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <DARR_prot.incl>
            ;loop l3 < 100; RFDRmix < 10ms
#include <t1_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction


Start, d1 do:H

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row
#include <t1evol_prot.incl> 

#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;######################################################
;#           INEPT Excitation Polarization            #
;######################################################


                         (p3  pl2  ph1):H

;########################
;#    t1 evolution      #
;########################

if "go_t1>1"
{        
  (center                   
    (d0):H
    (p1 pl1 ph20 p2 ph21 p1 ph20):C
  )      
}
  
  d4
  (center (p2 pl1 ph3):C (p4 ph2):H )
  d4
  (ralign (p1 ph5):C     (p3 ph4):H )
  d5
  (center (p2 pl1 ph6):C (p4 ph7):H )
  d5

  0.3u cpds2:H ;decouple 1H during acquisition
  TauCC
  (p8:spf8 pl28 ph8):C
  TauCCmSP2
  (p9:spf9 pl29 ph20):C

  (p1 pl1 ph9):C              ;##########################
  10u                         ;#   DQ,ZQ filter step    #
  (p1 pl1 ph10):C              ;##########################

  TauCCmSP
  (p8:spf8 pl28 ph11):C
  TauCCmSP

  (p1 pl1 ph12):C   (p1 pl2):H  ;store

  d8
  ;subr PDSD_DARR()

  (p1 pl1 ph13):C   (p1 pl2):H  ;readout


  0.3u cpds2:H ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31
  1m do:H

d1 mc #0 to Start F1PH(ip1,id0) 

HaltAcqu, 1m
exit

ph1 = {0}*4 {2}*4      ; H exc
ph2 = 0                ; H echo
ph3 = 0                ; C echo 
ph4 = 3                ; H store
ph5 = 0 2              ; C exc
ph6 = 0                ; C echo
ph7 = 0                ; H echo
ph8 = 0 2 1 3 2 0 3 1  ; C-C echo (4-step)
ph9 = 1 3              ; C ZQF DQF
ph10= 0                ; C ZQF DQF
ph11= {0}*8 {1}*8 {2}*8 {3}*8 ; C-C echo
ph12= 0                ; C store
ph13= 2                ; C exc

ph20= 0
ph21= 1

ph31= 0 0 2 2 2 2 0 0
      2 2 0 0 0 0 2 2 

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C Presat (-DCpre)
;p6      : C soft 90  (pl26)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;sp0     : H CP pwr
;pl1     : C HP pwr
;pl2     : H HP pwr
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl26    : C soft 90 pwr
;d1      : recycle delay
;d8      : PDSD/DARR mix time
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam6  : C soft 90 purge pulse (e.g. eSNOB)
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) D1, DP, CDC, Cpre, purge
