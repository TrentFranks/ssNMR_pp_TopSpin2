;hX_Hecho

;###########################################################
;#                                                         #
;# Hecho -Proton Simple Spin Echo *T2* is ~0.6*Signal(t0)  #
;#   Use fmp.hRX_HT2 or fmp.hRX_T1T2 (with -DHT2) for 2D   #
;#                                                         #
;###########################################################

;Cross Polarization for X channel (13C)
;Proton spin echo with CP
;written by WTF
;Avance II+ version

;$COMMENT= H-X CP with Proton Hahn Spin Echo period
;$CLASS=Solids
;$DIM=1D
;$TYPE=CPMAS
;$SUBTYPE=Setup
;$OWNER=Guest

define delay ONTIME
;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"
                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################
Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p10"

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H
                         
			 (p3  pl2 ph11):H 
                         (d6)
                         (p4  pl2 ph0):H 
                         (d6)
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
  
  0.3u cpds2:H ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31         
  1m do:H 
  d1 mc #0 to Start F0(zd)
  
HaltAcqu, 1m
exit

ph0 = 0                ; H CP Spin lock
ph1 = 1 3              ; H Hard Pulse
ph2 = 0 0 2 2 1 1 3 3  ; X CP Spin Lock 
ph11= 3 1              ; H90 Inversion

ph20= 0 1 2 3 

ph31= 0 2 2 0 1 3 3 1  ; receiver


;#######################################################

;PARAMETERS:
;p3      : H 90  (pl2)
;p4      : H 180 (pl2)
;p10     : H-C CP at pl10 (C) and sp0 (H)
;pl2     : H HP pwr level
;pl10    : X CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl20    : H CP pwr
;d1      : recycle delay
;d6      : Echo delay -Decho 
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam8  : soft X 180 e.g. rSnob
