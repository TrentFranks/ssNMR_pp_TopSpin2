;hcCcNH Proton-Detect CP x-fer experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#  hCcNH 3D for solids                               #
;#  Carbon-Carbon J-based mixing		      						#	
;#  Note: now with a freq hop before the specific cp  #
;#  made the s180 always on for phase cycle reasons   #
;######################################################
;
;$COMMENT= hCcNH Proton-Detect CP experiment
;$CLASS=Solids
;$DIM=3D
;$TYPE= 1H detect Hetero
;$SUBTYPE=CP Transfer
;$OWNER=guest
;
#include <HCN_defs.incl>
#include <D_defs.incl>
#include <Avance.incl> 
;for 2H decoupling

;######################################################
;#                Evolution Parameters                #
;######################################################

"in0=inf2"                  ;##########################
"d0=3u"                     ;#     t1_init => 0,0     #
"in30=inf2"                 ;# in10 is divided by 2		#											
"in10=inf1/2"               ;# because of the s180    #
"d10=in10/2-0.3u "          ;#   t2_init => 90,-180   #
"in29=inf1"                 ;##########################			

"cnst10=o2"									; to reset later

define loopcounter T1evo    ;############################
"T1evo=larger(td1,cnst29)"  ;#   Constant Duty Cycle    #
"d29=TauR+T1evo*in29"       ;#                          #
"d29=d29/2"                 ;# divide by 2 for mc macro #
define loopcounter T2evo    ;# Power Deposition Calcs   #
"T2evo=larger(td2,cnst30)"  ;#                          #
"d30=TauR+T2evo*in30"       ;############################
"d30=d30/2"                 ;# divide by 2 for mc macro #

define delay ONTIME

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

"cnst12=o2"
aqseq 321

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p3+p10+p1*2+p16+p11+p13+p21*2+p20+d29+d30+d18+d11*2+d12*2"

#include <1H_ON_prot.incl>
        ;total RF depsosition restriction
	;Protections for Proton Presat, H Acq, etc
#include <t1_prot_v2.incl>
        ;check t1 evolution time
#include <t2_prot_v2.incl>
        ;check t1 evolution time

;######################################################
;#              2H decoupling setup                   #
;######################################################
  30m LOCKDEC_ON
  30m LOCKH_ON
  30m H2_PULSE

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, 30m do:C do:N do:D

;######################################################
;#              2H lock                               #
;######################################################
  30m H2_LOCK
  30m LOCKH_OFF
  d1 
  30m LOCKH_ON
  30m H2_PULSE

;######################################################
;#               Protections: In Situ                 #
;######################################################

#include <t1evol_prot_v2.incl>
            ;check d0 and d30
#include <t2evol_prot_v2.incl>
            ;check d10 and d29

;######################
;#      PRESAT        #
;######################

#ifdef Pre
  3u pl18:H
  d18 cw:H ph20
  d13 do:H
#endif

;######################
;#      2H Dec       #
;######################
  0.5u pl5:D     ; WALTZ power on 2H
  1u  cpds5:D

;#####################
;#       HC CP       #
;#####################
  10u fq=cnst10(bf hz):C	    ;hop to CO

                         (p3  pl2 ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 


;############################
;# INEPT based CO-CA mixing #
;############################

  1u fq=cnst12(bf hz):C	    ;hop to center

  1u cpds2:H  
  (d11):C 									; tau for INEPT 1/4j ~4.7 ms ms
  (p2 pl1 ph3):C
  (d11):C 									; tau for INEPT 1/4j ~4.7 ms ms 
  (p1 pl1 ph4):C   
  
;##########################
;#  Time evolution on Cx  #
;##########################

;  1u fq=cnst11(bf hz):C			;carrier to CA
;  (d10):N
;  (center
;    (p8:spf8 pl28 ph15 p2 pl1 ph16):C     ;"CA soft hard pair
;    (p21 pl21 ph20 p22 ph21 p21 ph20):N
;  )   
;  1u fq=cnst12(bf hz):C	    ;hop to center
;  (d10):N

  (d10):N
  1u fq=cnst10(bf hz):C			;carrier to C0;
  (center
    (p9:spf9 pl29 ph15):C     ;"CO soft pulse"
    (p21 pl21 ph20 p22 ph21 p21 ph20):N
  )    
  (d10):N
 1u fq=cnst11(bf hz):C	    ;hop to CA
 (p8:spf8 pl28 ph16):C     ;"CA soft pulse"
  1u fq=cnst10(bf hz):C	    ;hop to CO
  (p9:spf9 pl29 ph17):C     ;"CO soft pulse"
  1u fq=cnst12(bf hz):C	    ;hop to center

;##########################
;# INEPT based CA-CO mixing #
;##########################

  (p1 pl1 ph5):C   
  (d11):C 									; tau for INEPT 1/4j ~4.7 ms
  (p2 pl1 ph6):C
  (d11):C 									; tau for INEPT 1/4j ~4.7 ms

;##########################
;# constant RF					  #
;##########################

  (p1 pl1 ph7):C
  d29
  (p1 pl1 ph8):C

;##########################
;# 	Frequency hop   			#
;##########################

1u fq=cnst10(bf hz):C	    ;hop to CO
  1u do:H

#ifdef opt
  1u cpds2:H
  (p9:spf9 pl29 ph23):C
  (p2 pl1 ph24):C
  1u do:H
#endif

;##########################
;#  	CN CP		  #
;##########################

 (p16:spf3 pl6 ph9):C (p16 pl13 ph1):H (p16:spf2 pl7 ph10):N  

  0.3u
  1u fq=cnst12(bf hz):C	    ;hop to center


;##########################
;#  Time evolution on Nx  #
;##########################

 1u cpds2:H

if "d0 > (p1*2 + p2)"
{
  (center
    (d0):N
    (p1 pl1 ph20 p2 ph21 p1 ph20):C
  )
}

  1u
  (p21 pl21 ph11):N
  d30
  1u do:H do:D


;###############
;# MISSISSIPPI #
;###############

  (p18 pl18 ph20):f1
  (p18 ph21):f1
  (p18 ph20):f1
  (p18 ph21):f1


;####################
;#  CP Back to 1H   #
;####################

  (p21 ph12):N
  (p13 pl24 ph13):N (p13:spf13 pl25 ph14):H
  0.5u pl3:N pl4:C     ; GARP power on 15N
  1u  cpds3:N cpds4:C cpds5:D

  go=Start ph31
  1m do:N do:C do:D

  30m mc #0 to Start
  	F1PH(rp11 & ip5 & rd0 & rd30, id10 & dd29)
		F2PH(ip11, id0 & dd30)

HaltAcqu, 1m

  30m H2_LOCK
  30m LOCKH_OFF
  30m LOCKDEC_OFF

exit

;#####################
;#  Phase Cycle, n>4 #
;#####################


ph20= 0; sat
ph21= 1; sat2
ph22= 2

;2 softpulse (opt) phase cycle saves the 3-4 steps for the 2nd soft pulse
;moves H90 to 5-8 and NC to 9-16
#ifdef opt
ph0= 0				  			;H HY
ph1= 1 1 3 3					;H90
ph2= 1 				  			;Y HY
ph3= 0 		    				;Y1801 
ph4= 1								;Y901
ph5 = 1								;Y902
ph6 =0								;Y1803 
ph7= 0  							;Y903
ph8= 2   							;Y904
ph9= 1								;Y YX 
ph10= 0 0 0 0 2 2 2 2	;X YX
ph11= 3								;X901
ph12= 1 							;X902
ph13= 0 							;X XH
ph14= 0	0 2 2					;H XH
ph15 =0 1			  			;CA soft 1
ph16 =0 							;CO soft 1
ph17 =0 							;CA soft 2
ph23= 	0 0 1 1 			;Y1802 (soft)
ph24=		0  						;Y1802 (hard)
ph31 = 0 2 0 2 2 0 2 0; Rec
#endif

#ifndef opt
ph0= 0				  			;H HY
ph1= 1 3    					;H90
ph2= 1 				  			;Y HY
ph3= 0 		    				;Y1801 
ph4= 1								;Y901
ph5 = 1								;Y902
ph6 =0								;Y1803 
ph7= 2  							;Y903
ph8= 0  							;Y904
ph9= 1								;Y YX 
ph10= 0 0 0 0 2 2 2 2	;X YX
ph11= 3								;X901
ph12= 1 							;X902
ph13= 0 							;X XH
ph14= 0	0 2 2					;H XH
ph15 =0 			  			;CA soft 1
ph16 =0 							;CO soft 1
ph17 =0 							;CA soft 2
ph31 = 0 2 2 0 2 0 0 2; Rec
#endif

;###############################################################
;PARAMETERS:
;p1      : C 90 at pl1
;p2      : C 180 at pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;p5      : H spinlock purge
;p10     : HC CP H pl20 C pl10
;p13     : NH CP H pl19 N pl11
;p16     : CN CP H pl13 C pl6 N pl7
;p18	 : 1H presat pl18
;p20     : Dream duration
;p21     : N 90 pl21
;p22     : N 180 pl21
;sp23    : Dream pwr
;d0      : initial t1 (3u)
;d1      : recycle delay
;d2      : total t1 ev time
;NS      : 2 * n
;DS      : >= 4
;pl1     : C 90/180 pwr level
;pl10	 : C HC cp pwr level
;pl16    : C CN cp pwr level
;pl4	 : C dec pwr level (not usually used)
;pl2     : H 90/180 pwr level
;pl13	 : H CN and DREAM dec pwr level
;pl17    : N CN cp pwr level
;pl18    : H presat pwr level
;pl19    : H NH cp pwr level
;pl20    : H HC cp pwr level
;pl21    : N 90/180 pwr level
;pl23    : C DREAM pl
;pl11    : N NH cp pwr level
;cpds3   : N dec cpdsprg3
;cpds4   : C dec cpdsprg4
;cnst10  : Freq for C after dream (Hz)
;cnst29  : expected td1 for use in constant Duty
;cnst30  : expected td2 for use in constant Duty
;cnst31  : MAS Rate
;zgoptns : (-D) Pre, PP, opt
