;CC with DQ DREAM mixing experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#  Dream:                                            #
;#  Verel, R; Ernst M; Meier, BH;                     #
;#     J. Magn. Reson. 2001, 150, 81-99.              #
;#                                                    #
;#  Adjust for MINIMUM                                #
;#  sp21 should vary about 0.5*wr,                    #
;#    e.g. if spinning at 20 kHz, the Dream Field     #
;#    is 10 kHz, then the ramp is generally about     #
;#    3.5kHz (+/-35% of total)                        #
;#                                                    #
;#  spnam20 should be a tangent ramp as described     #
;#  spnam18 Linear ramp from 0 to the beginning of    #
;#            sp20                                    #
;#  spnam19 Linear ramp from end of sp20 to 0         #
;#                                                    #
;#  sp21= |0.5*wr| ; p19  READ-IN/OUT ;               #
;#                                                    #
;#  p20 READ-IN/OUT ~= 0.5-1 rotor period             #
;#  p20 Mix duration  ~0.5 ms for 1 bond transfer     #
;#                                                    #
;#  Indirect Dimension phasing is 90,-180             #
;#                                                    #
;######################################################  
;
; Options DP, Cpre, CDC
; written Barth van Rossum, W Trent Franks

;$COMMENT= HC CP with C DQ mixing
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=DQ mix
;$OWNER=guest

; prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Mixing                    #
;######################################################

#include <fmp.DREAM.subr>

;cnst11 : adjust t=0 for acq, if digmod = baseopt
;"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-0.3u"             ;#   t1_init => 90,-180   #
"in30=inf1"                 ;#                        #
define loopcounter go_t1    ;#   No increment if 1D   #
"go_t1=td1"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;##########################
                            
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################
Prepare, ze

"d30=d30/2"                 ;# divide by 2 for mc macro #
"ONTIME=aq+d0+d30+p10+p8+2*p19+p20"

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <DREAM_prot.incl>
        ;loop l7 
#include <t1_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H 

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row                            

#include <t1evol_prot.incl> 

#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;######################################################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C 
#else
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
#endif

#ifndef D1                  ;##########################
  d0 cpds2:H                ;#      t1 evolution      #
  0.3u do:H                 ;##########################
#endif

;######################################################
;#              Homonuclear DREAM Mixing              #
;######################################################

  (p1 pl1 ph3):C   (p1 pl2):H  ;purge

  subr DREAM_(ph4)
  
  (p1 pl1 ph5):C   (p1 pl2):H  ;purge
    
  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	;start ADC with ph31 signal routing

#ifdef CDC
  d30           ; Constant duty cycle
#endif

1m do:H 	;decoupler off

lo to Start times ns

d1 mc #0 to Start F1PH(dp3,id0 & dd30) 

HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=8)             #
;#######################################################
ph0=  0
ph1=  1 3
ph2=  {0}*4 {2}*4
ph3=  0 2 2 0            ;Purge Pulse, no phase added
ph4=  0 0 2 2 
ph5=  0 0 0 0 2 2 2 2    ;Purge Pulse, no phase added

ph10= {1 3}*2^2          ; X90 DP

ph20= 0 2

ph31= 2 0 0 2   0 2 2 0

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C Presat (-DCpre)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;p19     : Read-in/out (0.5-1 rotor period)
;p20     : Dream duration
;sp0     : H CP pwr
;sp18    : Read-IN pwr
;sp19    : Read-OUT pwr
;sp20    : Dream pwr
;pl1     : C HP pwr
;pl2     : H HP pwr
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (During DREAM CW)
;d1      : recycle delay
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) D1, DP, CDC, Cpre
