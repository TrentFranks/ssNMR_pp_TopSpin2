;hN

;###############################################################
;# NT2                                                         #
;#      (15N) Hard-Pulse Spin Echo *T2* is ~0.6*Signal(t0)     # 
;#    Use appropriate (vd<10ms) vdlist with Topspin Analysis   #
;#       Analysis -> T1/T2 Relaxation Analysis -> uxnmrt2      #
;###############################################################

;Cross Polarization for Y channel (15N)
;Options for Direct Ex, DPCP, X90, Hahn echo, and Spin Lock periods
;written by WTF
;Avance II+ version

;$COMMENT= H-Y CP with N Hahn Spin Echo period
;$CLASS=Solids
;$DIM=2D
;$TYPE=CPMAS
;$SUBTYPE=Relaxation
;$OWNER=guest
define delay ONTIME
;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <N_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"d6=vd"
"ONTIME=aq+p11+2*d6"

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H 

"d6=vd"
"ONTIME=aq+p10+2*d6"

#include <Standard_Prot.incl>
        ;acq, CP time, etc.
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

                         (p3  pl2 ph1):H 
  (p11:spf1 pl11 ph2):N  (p11 pl19 ph0):H  

  0.3u cpds2:H  ;decouple 1H during acquisition
  
  vd            
  (p22  pl21  ph3):N 
  vd
  
  0.3u

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31         
  1m do:H 
  30m mc #0 to Start F1QF(ivd) 
  
HaltAcqu, 1m
exit

ph0 = 0
ph1 = 1 3
ph2 = 0 0 2 2 1 1 3 3
ph3 = 0 1 2 3 

ph31= 0 0 2 2 3 3 1 1  ; receiver

;#######################################################

;PARAMETERS: 
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;p11     : H-N CP contact pl11 (f1) and sp1 (f2)
;p16     : SPIN LOCK duration after CP 
;p21     : N 90 (pl21) use -Dp90  
;p22     : N 180 (pl21) use -Decho
;sp1     : H CP pwr
;pl2     : H HP pwr
;pl7     : N Spin Lock pwr -Dspinlock
;pl11    : N CP pwr 
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl21    : N HP pwr
;d1      : recycle delay
;d2      : H Inversion Recovery Time
;d6      : Echo delay -Decho
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam1  : use e.g. ramp.100 for variable amplitude CP
;zgoptns : (-D) None

