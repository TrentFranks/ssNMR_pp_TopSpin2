;HCC

;INEPT xfer with TOBSY mixing for C
;written by WTF
;Avance II+ version

;######################################################
;#                                                    #
;# H-C INEPT Transfer Experiment, C detect            #
;#                                                    #
;######################################################

;$COMMENT= HCC INEPT
;$CLASS=Solids
;$DIM=3D
;$TYPE=Heteronuclear
;$SUBTYPE=HCC Scalar Xfer
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Z-filter                  #
;######################################################

#include <fmp.DARR.subr>

define delay TauCC
define delay TauCCmSP
"TauCC=d21"
"TauCCmSP=d21-p8"

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-p3*2/3.14159"     ;#   t1_init => 90,-180   #
"in30=inf1"                 ;##########################

"in10=inf1/2"               ;##########################
"in29=inf1/2"               ;#                        #
"d10 =d21-0.3u-p8/2"        ;#     t1_init => 0,0     #
"d29 =0.3u"                 ;#   Definitions for CT   #
"d22 =d21-p8/2-p9"          ;#                        #
define delay ONTIME         ;##########################

define loopcounter go_t1
"go_t1=td1"

aqseq 312
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+d0+d30+p10+2*d21";

#include <Standard_Prot.incl>
        ;acq, CP time, etc.
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

                         (p3  pl2  ph1):H


;########################
;#    t1 evolution      #
;########################

if "go_t1>1"
{        
  (center                   
    (d0):H                  
    (p1 pl1 ph20 p2 ph21 p1 ph20):C
  )      
}
  
;########################
;#        INEPT         #
;########################
  
  d4
  (center (p2 pl1 ph3):C (p4 ph2):H )
  d4
  (ralign (p1 ph5):C     (p3 ph4):H )
  d5
  (center (p2 pl1 ph6):C (p4 ph7):H )
  d5

 0.3u cpds2:H

d10                         ;##########################
(p8:spf8 pl28 ph8):C        ;#   Scalar and t1 evol   #
d22                         ;# adjusted Tau(C-C) for  # 
(p9:spf9 pl29 ph20):C       ;#      soft pulses       #
d29                         ;##########################

(p1 pl1 ph9):C              ;##########################
1u                          ;#   DQ,ZQ filter step    #
(p1 pl1 ph10):C              ;##########################

TauCCmSP                    ;##########################
(p8:spf8 pl28 ph11):C       ;#  Scalar evol refocus   #
TauCCmSP                    ;##########################

0.3u do:H

;######################################################
;#              Z-filter step using DARR              #
;######################################################
 
  (p1 pl1 ph8):C   (p1 pl2):H  ;store
  
  subr PDSD_DARR()
  
  (p1 pl1 ph9):C   (p1 pl2):H  ;readout
    
  0.3u cpds2:H ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	           	;start ADC with ph31 signal routing

1m do:H 			;decoupler off

#ifdef CDC
  d30                   ; Constant
#endif

lo to Start times ns

30m mc #0 to Start F1PH(ip1,id0 & dd30) 
                   F2PH(rd0 & rp1 & dp5, id10 & dd29 & rd30)

HaltAcqu, 1m
exit
  
ph1 = 0 2              ; H exc
ph2 = 0                ; H echo
ph3 = 0                ; X echo 
ph4 = 3                ; H store
ph5 = 0 0 2 2          ; C exc
ph6 = 0                ; C echo
ph7 = 0                ; H exc
ph8 = 0 0 2 2  1 1 3 3 
ph9 = 1 1 3 3
ph10= 0
ph11= {0}*8 {1}*8 {2}*8 {3}*8
ph20= 0
ph21= 1


ph31= 2 0 2 0   0 2 0 2  0 2 0 2  2 0 2 0    ; receiver

;#########################################################################

;PARAMETERS:
;p1      : C 90 pl1
;p2      : C 180 pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;d1      : recycle delay
;d4      : HC INEPT time (1/4J ~ 1.5ms)
;d5      : HC INEPT time (1/4J ~ 1.5ms)
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
