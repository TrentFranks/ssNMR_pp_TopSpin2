;HC Proton-Detect INEPT x-fer experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#  HC Hetcor for solids                              #
;#                                                    #
;######################################################
;
;$COMMENT= HC INEPT Transfer Experiment
;$CLASS=Solids
;$DIM=1D
;$TYPE= 1H detect Hetero
;$SUBTYPE=CP Transfer
;$OWNER=guest
;
#include <HCN_defs.incl>

;######################################################
;#                Evolution Parameters                #
;######################################################

"in0=inf1"                  ;##########################
"d0=3u"                     ;#     t1_init => 0,0     #
"in2=inf1/2"                ;##########################

define loopcounter go_t1
"go_t1=td1"

define loopcounter T1evo 
"T1evo=larger(td1,cnst30)"

"d2=TauR+(T1evo*in2)"

define delay ONTIME

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

"d2=d2/2"                   ;# divide by 2 for mc macro #


;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p10+p12+p18*4+d2*2+d18"

#include <1H_ON_prot.incl>
        ;total RF depsosition restriction
; Protections for Proton Presat, H Acq, etc

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:C do:N

;######################
;#      PRESAT        #
;######################

#ifdef Pre
  3u pl18:H
  d18 cw:H ph20
  d13 do:H
#endif

;#####################
;#       HC CP       #
;#####################

                         (p3  pl2 ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 


;##########################
;#  Time evolution on Sx  #
;##########################
 

 1u cpds2:H
  (vd):C  
  (p2 pl1 ph3):C
  (vd):C

 (p1 pl1 ph4):C 
 1u do:H

;###############
;# MISSISSIPPI #
;###############

  (p18 pl18 ph20):f1
  (p18 ph21):f1
  (p18 ph20):f1
  (p18 ph21):f1


;####################
;#  CP Back to 1H   #
;####################
  
  1u 
  (p1 ph5):C
  (p12:spf12 pl10 ph6):C (p12 pl20 ph7):H

  0.5u pl3:N pl4:C     ; GARP power on 13C
  1u  cpds3:N cpds4:C

  go=Start ph31
  1m do:N do:C
  
  30m mc #0 to Start F1QF(ivd) 

HaltAcqu, 1m
exit

;#####################
;#  Phase Cycle, n>4 #
;#####################


ph0= 1 1		;H HX
ph1= 0 0 2 2		;H90
ph2= 0 0 0 0 2 2 2 2	;X HX
ph3= 1 1		;X180 (CT)
ph4= 3 3		;X901
ph5= 1 3		;X902
ph6= 0 0		;X XH
ph7= 1 1		;H XH

ph20= 0; sat
ph21= 1; sat2
ph22= 2

ph31= 3 1 1 3 1 3 3 1 ; Rec

;######################################################################

;PARAMETERS:
;p1      : C 90 at pl1
;p2      : C 180 at pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;p5      : H spinlock purge
;p21     : N 90 pl21
;p22     : N 180 pl21
;d0      : initial t1 (3u)
;d1      : recycle delay
;d2      : total t1 ev time
;NS      : 4 * n
;DS      : >= 4
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl21    : N HP pwr level
;cpds3   : N dec cpdprg3
;zgoptns : (-D) Pre, PP
