;hN

;######################################################
;#                                                    #
;# H-N CP with all the options                        #
;#                                                    #
;######################################################

;Cross Polarization for Y channel (15N)
;Options for Direct Ex, DPCP, X90, Hahn echo, and Spin Lock periods
;written by WTF
;Avance II+ version

;$COMMENT= H-Y CP with optional DP, Y90, Y-spinlock, and  Hahn Spin Echo periods
;$CLASS=Solids
;$DIM=1D
;$TYPE=CPMAS
;$SUBTYPE=Setup
;$OWNER=guest

define delay ONTIME
;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <N_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"
                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################
Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#ifdef HT2
"ONTIME=aq+p11+p16"
#else
"ONTIME=aq+p11+p16+2*d6"
#endif

#include <Standard_Prot.incl>
;      acquisition, CP time, PDSD/DARR, and echo restrictions
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H 

;#########################################################
;# Hir -Proton Inversion Recovery, Zero-Cross is ~0.6*T1 #
;#  Use fmp.hRX_HT1 or fmp.hRX_T1T2 (with -DHT1) for 2D  #
;#########################################################

#ifdef Hir
                    (p4 pl2 ph0):H 
  d2
#endif

;#######################################
;# DP -Direct Polarization to Carbons  #
;#######################################

#ifdef DP
  (p21 pl21 ph10):N 
#endif

;####################################################################
;# DPCP -Both Direct Polarization and Cross Polarization to Carbons #
;####################################################################

#ifdef DPCP
  (ralign (p21 pl1 ph10):N  (p3 pl2 ph1):H)
  (p11:spf1 pl11 ph2):N  (p11 pl19 ph0):H  

#endif

;###########################################################
;# Hecho -Proton Simple Spin Echo *T2* is ~0.6*Signal(t0)  #
;#   Use fmp.hRX_HT2 or fmp.hRX_T1T2 (with -DHT2) for 2D   #
;###########################################################

#ifdef HT2
                         (p3  pl2 ph11):H 
                         (d6)
                         (p4  pl2 ph0):H 
                         (d6)
  (p11:spf1 pl11 ph2):N  (p11 pl19 ph0):H  
#endif

;#################
;# Default Usage #
;#################

#ifndef HT2
#ifndef DPCP
#ifndef DP
                         (p3  pl2 ph1):H 
  (p11:spf1 pl11 ph2):N  (p11 pl19 ph0):H  
  
#endif
#endif
#endif

  0.3u cpds2:H  ;decouple 1H during acquisition

;#############################################
;# p90 Y (13C) Spin PIng *p21* is Zero Cross #
;#############################################

#ifdef p90
  (p21  pl21  ph3):N 
#endif

;###############################################################
;#  echo Y (13C) Hard-Pulse Spin Echo *T2* is ~0.6*Signal(t0)  #
;#     Does ~NOT~ compensate for scalar coupling evolution     #
;#     Use fmp.hRX_XT2 or fmp.hRX_T1T2 (with -DCT2) for 2D     #
;###############################################################

#ifdef echo
  d6               
  (p22  pl21  ph2):N 
  d6
  0.3u
#endif

;##############################################################
;#          Spinlock Y (13N) to find R3 conditions            #
;#                                                            #
;#     15N labeled systems: 1st minimum is 1*wr, 2nd min      #
;#       is 2*wr; should be no 3rd min (incresing pwr);       #
;#    15N should have no 1/2*wr minimum (HORROR condition)    #
;##############################################################

#ifdef spinlock 
  0.3u            ;DO NOT REMOVE!!!!
  0.3u do:H
  (p16 pl7 ph2):N  (p16 pl13 ph1):H
  0.3u cpds2:H 
#endif

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31         
  1m do:H 
  30m mc #0 to Start F0(zd)
  
HaltAcqu, 1m
exit

ph0 = 0
ph1 = 1 3
ph2 = 0 0 2 2 1 1 3 3
ph3 = 1 1 3 3 2 2 0 0  ; Y90
ph10= 1 3 3 1 2 0 0 2  ; Y90 DP
ph11= 3 1              ; H90 Inversion

ph20= 0 1 2 3 

#ifndef echo
#ifndef echoCW
  ph31= 0 2 2 0 1 3 3 1  ; receiver
#endif
#endif

#ifdef echo
  ph31= 0 0 2 2 3 3 1 1  ; receiver
#endif

#ifdef echoCW
  ph31= 0 0 2 2 3 3 1 1 ; receiver
#endif



;#########################################################################

;PARAMETERS: 
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;p11     : H-N CP contact pl11 (f1) and sp1 (f2)
;p16     : SPIN LOCK duration after CP 
;p21     : N 90 (pl21) use -Dp90  
;p22     : N 180 (pl21) use -Decho
;sp1     : H CP pwr
;pl2     : H HP pwr
;pl7     : N Spin Lock pwr -Dspinlock
;pl11    : N CP pwr 
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl21    : N HP pwr
;d1      : recycle delay
;d2      : H Inversion Recovery Time
;d6      : Echo delay -Decho
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam1  : use e.g. ramp.100 for variable amplitude CP
;zgoptns : (-D) DP, DPCP, p90, echo, spinlock, Hir, Hecho
