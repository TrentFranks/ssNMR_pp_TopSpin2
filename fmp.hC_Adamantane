;hC

;Cross Polarization for X channel (13C)
;written by WTF
;Avance II+ version

;######################################################
;#                                                    #
;# H-C Direct/Cross Polarization Experiment           #
;#                                                    #
;#  Adjust pl10, pl20, and p10 for maximum signal.    #
;#  The Hartman-Hahn CP condidition is B1(H)=B1(C)+-1 #
;#  sp0 is usually a linear or tangent ramp           #
;#  p10 is usually 1 ms                               #
;#                                                    #
;# Additional Options for Direct Excitation           #
;#  Allows 300ms acquisition                          # 
;#                                                    #
;######################################################

;$COMMENT= H-X CP with all options
;$CLASS=Solids
;$DIM=1D
;$TYPE=CPMAS
;$SUBTYPE=Setup
;$OWNER=Guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>

#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <acq_prot_Admntn.incl>
#include <d6_Admntn_prot.incl>
#include <p10_prot.incl>
			;acquisition, CP time and echo restrictions

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

#ifdef DP
  (p1 pl1 ph10):C
#else
                         (p3  pl2 ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
#endif
  
  0.3u cpds2:H ;decouple 1H during acquisition

#ifdef echo
  d6               
  (p2  pl1  ph20):C
  d6
#endif

;#######################################################
;#                     Acquisition                     #
;#######################################################


  go=Start ph31         
  1m do:H
  d1 mc #0 to Start F0(zd)
  
HaltAcqu, 1m
exit

ph0 = 0                ; H CP Spin lock
ph1 = 1 3              ; H Hard Pulse
ph2 = 0 0 2 2 1 1 3 3  ; X CP Spin Lock 
ph10= 1 3 3 1 2 0 0 2  ; X90 DP

ph20= 0 2 1 3 

#ifdef echo
  ph31= 0 2 0 2 1 3 1 3  ; receiver
#else
  ph31= 0 2 2 0 1 3 3 1  ; receiver
#endif

;######################################################

;PARAMETERS:
;p0      : C PRE
;p1      : C 90 (pl1)  use -Dp90 
;p2      : C 180 (pl1) use -Decho
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;p8      : C soft 180 (spnam8, pl28) use -DechoSP
;p10     : H-C CP at pl10 (f1,X) and sp0 (f2,H)
;p16     : SPIN LOCK duration after CP 
;pl0     : C PRE pwr level
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl6     : C Spin Lock pwr -Dspinlock
;pl28    : C soft 180 
;sp0     : H CP pwr
;pl10    : X CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;d1      : recycle delay
;d2      : H Inversion Recovery Time
;d6      : Echo delay -Decho 
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam8  : soft X 180 e.g. rSnob
;zgoptns : (-D) DP, echo