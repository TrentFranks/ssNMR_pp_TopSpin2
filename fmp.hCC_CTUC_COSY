;CC PDSD/DARR experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#  CTUC COSY (CA-CO opt)                             #
;#  Chen, L.; Olsen, R.; Elliot, D; Boettcher, J;     #
;#    Zhou, D; Rienstra, C; and Mueller, L.           #
;#    J Am Chem Soc 2006, 128, 9992-9993.             #
;#                                                    #
;#  ns = n*4. 16 is full cycle                        #
;#                                                    #
;######################################################
;
; Options DP, Cpre
; written W Trent Franks

;$COMMENT= HC CP with C COSY mixing
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=ZQ mixing
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Mixing                    #
;#           use Selective 180's w/ option            #
;######################################################

define delay TauCC
define delay Delta1
define delay TauCCmSP
define delay TauCCmSP2
define delay Delta1_mSP

#ifdef cmpd
"TauCC=d21-p1-p2/2"
#endif
#ifndef cmpd
"TauCC=d21-p2/2"
#endif

"Delta1=d21-d5-d5"
"TauCCmSP=d21-p8/2"
"TauCCmSP2=d21-p8/2-p9"
"Delta1_mSP=d21-d5-d5-p8/2"

"d30=0.3u"

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"


;######################################################
;#                   Define Z-filter                  #
;######################################################

#include <fmp.DARR.subr>

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

"in0 =inf1/2"               ;##########################
"in30=inf1/2"               ;#     t1_init => 0,0     #
"d22 =d21-p8/2-p9"          ;#                        #
define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
                            ;##########################
#ifdef SP
  "d0=d21-0.3u-p8/2"
  "d30=0.3u"
  "d29=T1evo*in0/2+p8/2"
#endif

#ifndef SP
  "d0=d21-0.3u-p2/2"
  "d30=d21-0.3u-p2/2-p1"
  "d29=T1evo*in0/2+p2/2+p1"
  "Delta1=Delta1_mSP"
#endif

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################
Prepare, ze

"ONTIME=aq+p8+p10+4*d21-Delta1";

;######################################################
;#               Protections: Pre-Check               #
;###############################88#######################

if "d29<d21" goto PassCC
  1m 
  print "t2 (d0) became larger than the delay (d21), aborted"
  goto HaltAcqu
PassCC, 1m

if "Delta1>10u" goto PassDelta1
  1m
  print "Refocussing time Delta1 (d21-2*d5) became too short, aborted"
  goto HaltAcqu
PassDelta1, 1m

#include <Standard_Prot.incl>
        ;acquisition, CP time, P8DSD/DARR, and echo restrictions
#include <DARR_prot.incl>
        ;Z-filter protections
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;###############################8#######################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row
#include <t1evol_prot.incl>

#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;##############################8#######################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C

#endif

#ifdef DPCP
#ifndef DP
  (p1 pl1 ph10):C
#endif
#endif

#ifndef DP
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph3):C  (p10 pl20 ph2):H
#endif

0.3u cpds2:H

;######################################
;#    Constant time t1 evolution      #
;######################################

;### Option for aliphatic selective pulse version ###
#ifdef SP
    d0
    (p8:spf8 pl28 ph4):C
    d22
    (p9:spf9 pl29 ph20):C
    d30:C
#endif

;### Option for compound pulses ###
#ifndef SP
#ifdef cmpd
    d0
    (p1 pl1 ph14 p2 ph4 p1 ph14):C
    d30
#endif
#endif

;### Default is single hard pulses ###
#ifndef SP
#ifndef cmpd
    d0
    (p2 pl1 ph4):C
    d30
#endif
#endif


  (p1 pl1 ph5):C              ;##########################
  10u                         ;#   DQ,ZQ filter step    #
  (p1 pl1 ph6):C              ;##########################

#ifndef SP
#ifndef cmpd
  TauCC
  (p2 pl1 ph7):C
  TauCC
#endif
#endif

#ifndef SP
#ifdef cmpd
  TauCC
  (p1 pl1 ph17 p2 ph7 p1 ph17):C
  TauCC
#endif
#endif

#ifdef SP
  TauCCmSP
  (p8:spf8 pl28 ph6):C
  TauCCmSP
#endif

0.3u do:H

;######################################################
;#              Z-filter step using DARR              #
;######################################################

  (p1 pl1 ph8):C   (p1 pl2):H  ;store

  subr PDSD_DARR()

  (p1 pl1 ph9):C   (p1 pl2):H  ;readout

  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;###############################8########################

gosc ph31 	           	;start ADC with ph31 signal routing

1m do:H 				;decoupler off
lo to Start times ns

30m mc #0 to Start F1PH(dp3 & dp10,dd0 & id30) 

HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=16)            #
;#######################################################

;### After Mueller et al.
ph1=  1                ; H exc
ph2=  0                ; H spin lock
ph3=  0 2              ; C spin lock
ph4=  0 2 1 3 2 0 3 1  ; C echo CC
ph5=  1 3              ; C ZQDQ
ph6=  0                ; C ZQDQ
ph7=  {1}*8 {2}*8  {3}*8 {0}*8   ; C echo
ph8=  0                ; C store (z-filter)
ph9=  3                ; C exc   (z-filter)

ph10= 1 3              ; Direct Pulse phasing

ph14= 0 2 2 0 3 1 1 3  ; C cmpd pi (ph3)
ph17= {3}*8 {0}*8  {1}*8 {2}*8   ; C cmpd pi (ph6)

ph20= 0
ph21= 1

ph31= 0 0 2 2 0 0 2 2   2 2 0 0 2 2 0 0


;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p6      : C soft 90  (pl26)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;sp0     : H CP pwr
;pl1     : C HP pwr
;pl2     : H HP pwr8
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl26    : C soft 90 pwr
;d1      : recycle delay
;d8      : PDSD/DARR mix time
;d21     : TauCC (5ms)
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) DP, DPCP, SP, cmpd
