;hC CP with TMREV (TopSpin 2.3)

;Use cpd7 and cpd8 as TMREV and TMREV_180

;######################################################
;#                                                    #
;#                                                    #
;######################################################
;  
; Options DP, Cpre, CDC
; written W Trent Franks

;$COMMENT= HC CP with HC dipolar recoupling
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=Dipolar Dephasing
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                 Define Dephasing                   #
;######################################################

#include <fmp.TMREV.subr>

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

define delay ONTIME  
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

"ONTIME=aq+p10+p8+maxT_TMREV"

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <t1_prot.incl>
        ;check t1 evolution time
#include <TMREV_prot.incl>
        ;Recoupling restrictions
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

if "td1 <= (maxTMREV+1)" goto PassMaxTMREV
  1m 
  print "TMREV evolution (td1) too large: max td1 = 1+(cnst5*cnst6)"
  goto HaltAcqu
PassMaxTMREV, 1m

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

"l10=0"

Start, d1 do:H

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row
#include <t1evol_prot.incl>

subr EVOL_TMREV(l10)

#ifdef Cpre
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;######################################################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C
#else
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
#endif

;######################################################
;#            Heteronuclear TMREV recoupling          #
;##########################!###########################

#ifdef SP
  subr C_TMREV_SP(ph3, l10)
#else
  subr C_TMREV(ph3, l10)
#endif

0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31 	; Start ADC with ph31 signal routing
  1m do:H 	; Decoupler off
  d1 mc #0 to Start F1QF(iu10)
                  
HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=16)            #
;#######################################################

ph0=  0
ph1=  {1}*4 {3}*4
ph2=  {0}*2 {2}*2
ph3=  0 1            ;TMREV 180

ph10= {1}*4 {3}*4 {3}*4 {1}*4  ; X90 DP

ph31= 0 2 2 0  2 0 0 2

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p7      : C Soft Refocussing Pulse
;p0      : C Presat (-DCpre)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;sp0     : H CP pwr
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl5     : H TMREV pwr level
;pl28    : C Soft Refocussing PWR
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;cnst5   : Symmetry (inc/rotor period)
;cnst6   : Base Rotor periods
;cnst7   : Multiplier 
;pl15    : C SPC5 pwr
;l5      : C5 repetitions n*5 (doubled)
;d1      : recycle delay
;SPC53mix: mix time POST-C7 (half)
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) D1, DP, CDC, Cpre
