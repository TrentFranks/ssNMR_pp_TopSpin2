;hX_Xecho

;######################################################
;#                                                    #
;# H-C Cross Polarization Experiment                  #
;#                                                    #
;#   echo -X (C) Spin Echo *T2* is ~0.6*Signal(t0)    #
;#   Does ~NOT~ compensate for Jcoupling by Default   #
;#  fmp.hRX_XT2 or fmp.hRX_T1T2 (with -DCT2) for 2D   #
;#                                                    #
;# s180 -X (C) Soft Spin Echo *T2* is ~0.6*Sig(t0)    #
;# ~DOES~ compensate for J-coupling evolution if set  #
;#                                                    #
;# fmp.hRX_XT2 or fmp.hRX_T1T2 (with -DCT2SP) for 2D  #
;######################################################

;Cross Polarization for X channel (13C) with Carbon Echo
;Options for soft pi pulse
;written by WTF
;Avance II+ version


;$COMMENT= H-C CP with C-Hahn Spin Echo period
;$CLASS=Solids
;$DIM=1D
;$TYPE=CPMAS
;$SUBTYPE=Setup
;$OWNER=Guest
;prosol relations=<solids_cp>

define delay ONTIME
;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"
                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p10+2*d6"

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
  
  0.3u pl13:H
  0.3u cpds3:H ;decouple 1H during echo

;######################################################
;#            Hard or Selective Pulse                 #
;######################################################
  
  d6               
#ifdef s180
  (p8:spf8 pl28 ph3):C
#else
  (p2 pl1 ph3):C
#endif
  d6

  0.3u do:H
  0.3u cpds2:H ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31         
  1m do:H
  30m mc #0 to Start F0(zd)
  
HaltAcqu, 1m
exit

ph0 = 0                ; H CP Spin lock
ph1 = 1 3              ; H Hard Pulse
ph2 = 0 0 2 2 1 1 3 3  ; X CP Spin Lock
ph3 = 0 1 2 3 

ph31= 0 0 2 2 3 3 1 1  ; receiver

;#######################################################


;PARAMETERS:
;p1      : C 90 (pl1)  use -Dp90 
;p2      : C 180 (pl1) use -Decho
;p3      : H 90 at pl2
;p8      : C soft 180 (spnam8, pl8) use -DechoSP
;p10     : H-C CP at pl10 (C) and sp0 (H)
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl8     : C soft 180 
;sp0     : H CP pwr
;pl10    : X CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;d1      : recycle delay
;d6      : Echo delay -Decho 
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam8  : soft X 180 e.g. rSnob
;zgoptns : (-D) s180
