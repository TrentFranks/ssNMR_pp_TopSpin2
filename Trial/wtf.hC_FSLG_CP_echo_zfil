;HC CP with FSLG for 13C 
;written by WTF
;Avance II+ version

;######################################################
;#                                                    #
;# H-C LG-CP Transfer Experiment, C detect            #
;#   FSLG during 1H time evolution.  Experimental     #
;#                                                    #
;#  Indirect Dimension phasing is 0,0                 #
;#                                                    #
;#  F1(t1) H; F2 (t2) C                               #
;######################################################

;$COMMENT= HC CP with homonuclear 1H decoupling 
;          during 1H echo and CP transfer
;$CLASS=Solids
;$DIM=1D
;$TYPE=Heteronuclear
;$SUBTYPE=CP Xfer
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

;######################################################
;#                   Define FSLG                      #
;#    Constants and pulses needed for Lee-Goldburg    #
;######################################################

;cnst20=applied RF field in Hz (w1)

;this is (-sr) from topspin
"cnst21=0"

"cnst22= cnst20/sqrt(2)+cnst24" 	;cnst20=desired RF field in Hz
"cnst23=-cnst20/sqrt(2)-cnst24" 	;negative offset is cnst23
;"cnst22= cnst20/sqrt(2)+cnst24+2000"	;cnst20=desired RF field in Hz
;"cnst23=-cnst20/sqrt(2)-cnst24+2000"	;negative offset is cnst23

define pulse pFSLG
define pulse pMA
define pulse pMAc
define delay ONTIME

"pFSLG=294000000u/(360*cnst20)"
"pMA =p3*547/900"
"pMAc=p3*353/900"

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+(l10*4*pFSLG)+p10"

#include <Standard_Prot.incl>
        ;acq, CP time, etc.
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

(10u fq=cnst21):H

(p3  pl2  ph1):H

1u

;############################################
;#         t1 evolution with FSLG           #
;############################################

FSLG1,
 (pFSLG ph20 fq=cnst22 pl13):H
  pFSLG:H ph22 fq=cnst23:H
  pFSLG:H ph20 fq=cnst22:H 
  pFSLG:H ph22 fq=cnst23:H
  lo to FSLG1 times l10

1u fq=cnst21:H
(p4 pl2 ph2):H
1u

FSLG2,
 (pFSLG ph20 fq=cnst22 pl13):H
  pFSLG:H ph22 fq=cnst23:H
  pFSLG:H ph20 fq=cnst22:H 
  pFSLG:H ph22 fq=cnst23:H
  lo to FSLG2 times l10

1u fq=cnst21:H
;(p5 pl2 ph3):H     ;Realign tilted frame with lab frame

0.5u

  (p10:spf0 pl10 ph5):C  (p10 pl20 ph4):H 

  1u fq=cnst21:H 
  1u cpds2:H       ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	           	;start ADC with ph31 signal routing

1m do:H 			;decoupler off

lo to Start times ns

30m mc #0 to Start F0(zd) 

HaltAcqu, 1m
exit

ph1 = 0 2              ; H exc
ph2 = 0                ; H echo
ph3 = 3                ; H MA
ph4 = 1 1 3 3                ; H spin-lock
ph5 = {0}*4 {2}*4 {1}*4 {3}*4      ; C spin-lock

ph20= 0
ph21= 1
ph22= 2

ph31= 0 2 2 0   2 0 0 2 
      1 3 3 1   3 1 1 3 ; receiver

;#########################################################################

;PARAMETERS:
;p1      : C 90 pl1
;p2      : C 180 pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;d1      : recycle delay
;d4      : HC INEPT time (1/4J ~ 1.5ms)
;d5      : HC INEPT time (1/4J ~ 1.5ms)
;cnst21  : 0 Hz (on resonance)
;cnst20  : LG-RF 1H field strength, in Hz
;cnst22  : (+)LG-offset, calculated from cnst20 and cnst24 
;cnst23  : (-)LG-offset, calculated from cnst20 and cnst24
;cnst24  : additional 1H offset under LG, usually 0 - -2000
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
