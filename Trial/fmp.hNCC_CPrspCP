; hNCC3D with DARR mixing (and PDSD)
; MINIMUM phase cycle= 4
;
;######################################################
;#                                                    #
;# N-C SPECIFIC CP                                    #
;# Baldus, M.; Petkova, A. T.; Herzfeld, J. H.;       #
;#    Griffin, R. G. Mol. Phys. 1998,95, 1197-1207.   #
;# M. Hong and R.G. Griffin;                          #
;#    J. Am. Chem. Soc. 120, 7113-7114                #
;#                                                    #
;#  pl7 (15N) = 5*wr/2                                #
;#  pl6 (13C) = 3wr/2(NCA) 7wr/2(NCO)                 #
;#                                                    #
;# N-C PAIN CP (use square pulse shape)               #
;#  J. Lewandowski, G. De Pape, and                   #
;#     R. G. Griffin; J Am Chem Soc. 129(4): 728-29   #
;#                                                    #
;#  pl7 (15N) = 5*wr/2                                #
;#  pl6 (13C) = 5*wr/2  and various other conditions  #
;#                                                    #
;######################################################;
;
; HN-CP, then NC-CP, then CC mixing
; written by W Trent Franks
; FMP Berlin
;
;$COMMENT=hNCC Double CP 
;$CLASS=Solids
;$DIM=3D
;$TYPE=Heteronuclear
;$SUBTYPE=hNCC DCP
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>
#include <fmp.RespirationCP.subr>

;######################################################
;#                   Define Mixing                    #
;#  PDSD/DARR is Default, add Selective 90 w/ option  #
;######################################################

#include <fmp.DARR.subr>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-0.3u-p1*2/3.14159";#   t1_init => 90,-180   #
"in30=inf1"                 ;##########################

"in10=inf2"                 ;##########################
"d10=in10/2-0.3u "          ;#   t2_init => 90,-180   #
"in29=inf2"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;#                        #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;#                        #
define loopcounter T2evo    ;# Power Deposition Calcs #
"T2evo=larger(td2,cnst29)"  ;#                        #
"d29=T2evo*in29"            ;##########################

define loopcounter go_t1
"go_t1=td1"
define loopcounter go_t2
"go_t2=td2"
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

aqseq 312

Prepare, ze

"d30=d30/2"                 ;#divide by 2 for mc macro#
"d29=d29/2"                 ;#divide by 2 for mc macro#

"ONTIME=aq+d0+d30+d10+d29+p11+p16+p8" ;# DARR not in  #

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
       ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <DARR_prot.incl>
        ;d8
#include <t1_prot.incl>
        ;check t1 evolution time
#include <t2_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
#include <HN_RESPIRATION_prot.incl>
#include <NC_RESPIRATION_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

;######################################################
;#               Protections: In Situ                 #
;######################################################

#include <t1evol_prot.incl>
            ;check d0 and d30
#include <t2evol_prot.incl>
            ;check d10 and d29

#ifdef Cpre
 ; Carbon Presatuaration
 (p8 pl8 ph20):C
#endif

;######################################################
;#                Initial excitation                  #
;######################################################

                         (p3  pl2  ph1):H
  subr HN_RESPIRATION(ph2)
;                         (p11 pl19 ph0):H (p11:spf1 pl11 ph2):N

0.3u

if "go_t2>1"
{                           ;##########################
  0.3u cpds2:H              ;#      t1 evolution      #
                            ;##########################
  (center
    (d10):N 
    (p1 pl1 ph20 p2 ph21 p1 ph20):C 
  )
  0.3u do:H ;

}

;######################################################
;#             Second CP Period (SPECIFIC)            #
;######################################################
;
; (p16:spf2 pl6 ph4):C (p16 pl13 ph1):H (p16 pl7 ph3):N
;
;######################################################
;#           Second CP Period (Respiration)           #
;######################################################

 0.3u cpds2:H
; subr NC_Nlck_RESPIRATION(ph4)
 subr NC_Clck_RESPIRATION(ph4)
 0.3u do:H

if "go_t1>1"
{                           ;##########################
  0.3u cpds2:H              ;#      t2 evolution      #
                            ;##########################
  (center
    (d0):C 
    (p21 pl21 ph20 p22 ph21 p21 ph20):N
  )
  0.3u do:H 
}

;######################################################
;#            Homonuclear DARR/PDSD Mixing            #
;######################################################
  
  (p1 pl1 ph5):C   (p1 pl2):H  ;store

#ifdef purge            
  subr DARRs6_(ph20)   ; soft 90 (s6) with option
#else
  subr PDSD_DARR()
#endif
  
  (p1 pl1 ph6):C   (p1 pl2):H  ;readout
    
  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 		;start ADC with ph31 signal routing

#ifdef CDC
  d29                   ; T1 (d10) Nitrogen Const. Duty
  d30                   ; T2 (d0) Carbon Const. Duty
#endif

1m do:H 				;decoupler off
lo to Start times ns

d1 mc #0 to Start F1PH(dp5, id0 & dd30) 
                  F2PH(rd0 & rp5 & ip2, id10 & dd29 & rd30)

HaltAcqu, 1m
exit

ph0 = 0
ph1 = 1 1 1 1  1 1 1 1  3 3 3 3  3 3 3 3
ph2 = 0 2 0 2  
ph3 = {0}*16 {2}*16 
ph4 = 0 0 2 2 
ph5 = 3 3 3 3  1 1 1 1
ph6 = 1 1 2 2

ph31= 0 2 3 1  2 0 1 3  2 0 1 3  0 2 3 1
;      2 0 1 3  0 2 3 1  0 2 3 1  2 0 1 3
ph20= 0
ph21= 1
ph22= 2

;#######################################################

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p4      : H 180 (pl2)
;p6      : C soft 90  (pl26)
;p8      : C Presat (-DCpre)
;p11     : HN CP contact pl11 (N) and sp1 (H)
;p16     : NC CP contact pl7 (N) and pl8 (C) pl13 H
;p21     : N 90 
;p22     : N 180

;pl1     : C HP pwr
;pl2     : H HP pwr
;pl6     : C NC-CP pwr
;pl7     : N NC-CP pwr 
;pl11    : N HN CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl21    : N HP pwr
;pl26    : C soft 90 pwr

;d8      : PDSD/DARR mix time
;pcpd2   : H dec pulse
;pcpd3   : RFDR dec pulse 

;spnam1  : HN CP file
;spnam2  : NC CP shape (shape on C)
;spnam6  : C soft 90 purge pulse (e.g. eSNOB)
;sp1     : H CP pwr (HN) (p11)
;o1 	 : CA or CO frequency
;cnst1 	 : o1+cnst1=offset for specific CP
;cnst2 	 : offset for last X pulse
;d1      : recycle delay
;in0 	 : 1/swh 15N t1 dimension
;cpdprg2 : Decoupling file cw, tppm (at pl12)
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) CDC, Cpre, purge
