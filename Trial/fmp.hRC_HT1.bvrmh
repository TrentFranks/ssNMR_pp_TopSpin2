;hRX_HT1

;#####################################################
;# -Proton Inversion Recovery Zero-Cross is ~0.6*T1  #
;#   Use appropriate vdlist with Topspin Analysis    #
;# Analysis -> T1/T2 Relaxation Analysis -> invrec   #
;#####################################################

;Cross Polarization for X channel (13C)
;Measurement of H T1 with vd list by inversion recovery
;written by WTF
;Avance II+ version

;$COMMENT= H-X CP with H Inversion-Recovery
;$CLASS=Solids
;$DIM=2D
;$TYPE=CPMAS
;$SUBTYPE=Relaxation
;$OWNER=Guest
;prosol relations=<solids_cp>

define delay ONTIME
;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################

1 ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p10"
#include <Standard_Prot.incl>
        ;acq, CP time, etc.
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

2 d1 do:H

                         (p4 pl2 ph0):H 
  vd
                         (p3  pl2  ph1):H
  (p10:spf31 pl10 ph2):C  (p10:spf32 pl20 ph0):H 
  
  0.3u cpds2:H ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=2 ph31         
  1m do:H 
  d1 mc #0 to 2 F1QF(ivd) 

HaltAcqu, 1m
exit

ph0 = 0                ; H CP Spin lock

ph1 = 1 3              ; H Hard Pulse
ph2 = 0 0 2 2 1 1 3 3  ; X CP Spin Lock 

ph3 = 1 1 3 3 2 2 0 0  ; C to minus Z
ph4 = 3 3 1 1 0 0 2 2  ; C back to starting point

ph11= 3 1              ; H90 Inversion

ph20= 0 1 2 3

ph31= 0 2 2 0 1 3 3 1  ; receiver

;#####################################################

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p4      : H 180 (pl2)
;p10     : H-C CP at pl10 (C) and sp0 (H)
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl10    : X CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl20    : H CP pwr
;d1      : recycle delay
;d6      : Echo delay -Decho 
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
