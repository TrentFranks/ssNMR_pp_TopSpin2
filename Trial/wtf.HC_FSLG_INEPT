;HC INEPT for 13C 
;written by WTF
;Avance II+ version

;######################################################
;#                                                    #
;# H-C INEPT Transfer Experiment, C detect            #
;#   FSLG during INEPT period.  Experimental          #
;#                                                    #
;#  Indirect Dimension phasing is 0,0                 #
;#                                                    #
;#  F1(t1) H; F2 (t2) C                               #
;######################################################

;$COMMENT= HC INEPT with homonuclear 1H decoupling 
;          during INEPT transfer
;$CLASS=Solids
;$DIM=2D
;$TYPE=Heteronuclear
;$SUBTYPE=Scalar Xfer
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

;######################################################
;#                   Define FSLG                      #
;#    Constants and pulses needed for Lee-Goldburg    #
;######################################################

;cnst20=applied RF field in Hz (w1)

;this is (-sr) from topspin
"cnst21=0"

"cnst22= cnst20/sqrt(2)+cnst24"
"cnst23=-cnst20/sqrt(2)+cnst24"
;"cnst22=cnst20/sqrt(2)+cnst24+2000"	;cnst20=desired RF field in Hz
;"cnst23=-cnst20/sqrt(2)+cnst24-2000"	;negative offset is cnst23

define pulse pFSLG
define pulse pFSLGmp3u
define pulse pMA
define pulse pMAc
define delay dFSLG
"pFSLG=294000000u/(360*cnst20)"
"pFSLGmp3u=pFSLG-0.3u"
"pMA =p3*547/900"
"pMAc=p3*353/900"

"dFSLG=294000000u/(360*cnst20)"

;######################################################
;#                   Define Mixing                    #
;#  Use complete FSLG components during INEPT stages  #
;######################################################

define delay HCinept1
define delay HCinept2
define loopcounter FSLG1
define loopcounter FSLG2

                            ;##########################
"FSLG1=d4/(4*pFSLG)"        ;# FSLG loops in d4       #
"FSLG2=d5/(4*pFSLG)"        ;# FSLG loops in d5       #
"HCinept1=(FSLG1*4*pFSLG)"  ;# d4 to nearest full loop#
"HCinept2=(FSLG2*4*pFSLG)"  ;# d5 to nearest full loop#
                            ;##########################

"in0=inf1"                  ;##########################
"d0=in0/2-p3*2/3.14159"     ;#   t1_init => 90,-180   #
"in30=inf1"                 ;#                        #
define loopcounter go_t1    ;#   No increment if 1D   #
"go_t1=td1"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;##########################

                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+p3+p4+p3+p4+aq+(2*d4)+(2*d5)"

#include <Standard_Prot.incl>
        ;acq, CP time, etc.
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

(10u fq=cnst21):H

;#####################################################
;# Excite to the tilted-rot. x-frame, (pMA more sensitive)#
;#  INEPT period results in pure y-phase             #
;#####################################################

(p3  pl2  ph1):H

d4
;FSLGd4n1,
; (pFSLG ph20 fq=cnst22 pl13):H
;  pFSLG:H ph22 fq=cnst23:H
;  pFSLG:H ph20 fq=cnst22:H 
;  pFSLG:H ph22 fq=cnst23:H
;  lo to FSLGd4n1 times FSLG1

;1u fq=cnst21:H

;############################################
;#    t1 evolution with FSLG turned off     #
;############################################

  (center
    (d0):H
    (p1 pl1 ph21 p2 ph20 p1 ph21):C
  )

;  (pMA pl2 ph23):H             ;Tilt LG frame to Rotating frame
  (p4  ph2):H                  ;INEPT echo
;  (pMA ph21):H                 ;Tilt LG frame back to Rotating frame

;1u
d4
;FSLGd4n2,
; (pFSLG ph20 fq=cnst22 pl13):H
;  pFSLG:H ph22 fq=cnst23:H
;  pFSLG:H ph20 fq=cnst22:H 
;  pFSLG:H ph22 fq=cnst23:H
;  lo to FSLGd4n2 times FSLG1
;1u fq=cnst21:H

  ;Realign the Tilted-Rot frame with the Rot frame pi pulse and back
;  (pMA pl2 ph23):H
  (ralign (p1 ph5):C  (p3 ph4):H )
  (pMA ph21):H

;d5
FSLGd5n1,
 (pFSLG ph20 fq=cnst22 pl13):H
  pFSLG:H ph22 fq=cnst23:H
  pFSLG:H ph20 fq=cnst22:H 
  pFSLG:H ph22 fq=cnst23:H
  lo to FSLGd5n1 times FSLG2
1u fq=cnst21:H

  (center
    (p2 pl1 ph6):C
    (p4 pl2 ph7):H
  )
;    (pMA pl2 ph23 p4 ph7 pMA ph21):H

;d5
FSLGd5n2,
 (pFSLG ph22 fq=cnst23 pl13):H
  pFSLG:H ph20 fq=cnst22:H
  pFSLG:H ph22 fq=cnst23:H 
  pFSLG:H ph20 fq=cnst22:H
  lo to FSLGd5n2 times FSLG2
1u fq=cnst21:H

  0.3u cpds2:H ;decouple 1H during acquisition

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	           	;start ADC with ph31 signal routing

1m do:H 			;decoupler off

#ifdef CDC
  d30                   ; Constant
#endif

lo to Start times ns

30m mc #0 to Start F1PH(dp4,id0 & dd30) 

HaltAcqu, 1m
exit

ph1 = 0 2              ; H exc
ph2 = 0                ; H echo
ph3 = 0                ; X echo 
ph4 = 3                ; H store
ph5 = 0 0 2 2          ; C exc
ph6 = 0                ; C echo
ph7 = 0                ; H exc

ph20= 0
ph21= 1
ph22= 2
ph23= 3

ph31= 2 0 0 2          ; receiver

;#########################################################################

;PARAMETERS:
;p1      : C 90 pl1
;p2      : C 180 pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;d1      : recycle delay
;d4      : HC INEPT time (1/4J ~ 1.5ms)
;d5      : HC INEPT time (1/4J ~ 1.5ms)
;cnst21  : 0 Hz (on resonance)
;cnst20  : LG-RF 1H field strength, in Hz
;cnst22  : (+)LG-offset, calculated from cnst20 and cnst24 
;cnst23  : (-)LG-offset, calculated from cnst20 and cnst24
;cnst24  : additional 1H offset under LG, usually 0 - -2000
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
