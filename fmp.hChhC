;hChhC Proton-mixing experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#  CHHC                                              #
;#  Lange, A.; Becker, S.; Seidel, K.; Pongs, O.;     #
;#    and Baldus,M.  Angw. Chem. 2005, 44, 2089-92    #
;#  Lange, A.; Seidel, K.; Verdier, L.; Luca, S.;     # 
;#    and Baldus, M.,  JACS 2003, 125, 12640-48       #
;#  Lange, A.; Luca, S.; and Baldus, M.;              #
;#     JACS 2002, 124, 9704-5                         #
;#                                                    #
;#  ns = n*4. 32 is full cycle                        #
;#                                                    #
;#  Indirect Dimension phasing is 90,-180             #
;#                                                    #
;######################################################
;
; Options DP, purge, Cpre
; written Barth van Rossum, W Trent Franks

;$COMMENT= HC CPs with Transverse Proton Mixing
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=ZQ mixing
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <CH_defs.incl>

;######################################################
;#          Define Mixing Parameters                  #
;######################################################

#include <fmp.DARR.subr>

;cnst11 : adjust t=0 for acq, if digmod = baseoptC Z-mixing
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-0.3u"             ;#   t1_init => 90,-180   #
"in30=inf1"                 ;#                        #
define loopcounter go_t1    ;#   No increment if 1D   #
"go_t1=td1"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;##########################
 
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

"d30=d30/2"                 ;#divide by 2 for mc macro#
"ONTIME=aq+d0+d30+p10+p8+p9"   ;#  DARR left out  

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <DARR_prot.incl>
        ;d8
#include <t1_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row
#include <t1evol_prot.incl>

;######################################################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C 
#else
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
#endif

if "go_t1>1"
{                           ;##########################
  d0 cpds2:H                ;#      t1 evolution      #
  0.3u do:H                 ;##########################
}

;######################################################
;#      CP to H; flip to Z; Mix; CP back to C         #
;######################################################

  (p12:spf12 pl10 ph3):C (p12 pl20 ph4):H
  (lalign
    (p1 pl1 ph9):C
    (p3  pl2 ph5):H
  )

  d8

                          (p3  pl2 ph6):H
  (p14:spf14 pl10 ph8):C  (p14 pl20 ph7):H

;#######################################################
;#                     Acquisition                     #
;#######################################################

  0.3u cpds2:H 

gosc ph31 	           	;start ADC with ph31 signal routing

#ifdef CDC
  d30                   ; Constant duty cycle
#endif

1m do:H 				;decoupler off
lo to Start times ns

d1 mc #0 to Start F1PH(ip2,id0 & dd30) 

HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=16)            #
;#######################################################

ph0=  0
ph1=  1 3
ph2=  0
ph3=  0 0 2 2
ph4=  {1}*4 {3}*4
ph5=  {0}*8 {2}*8
ph6=  2
ph7=  {1}*16 {3}*16
ph8=  0 1
ph9=  1

ph31= 2 1 0 3 0 3 2 1    0 3 2 1 2 1 0 3 
      0 3 2 1 2 1 0 3    2 1 0 3 0 3 2 1

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C Presat (-DCpre)
;p6      : C soft 90  (pl26)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;sp0     : H CP pwr
;pl1     : C HP pwr
;pl2     : H HP pwr
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;pl26    : C soft 90 pwr
;d1      : recycle delay
;d8      : PDSD/DARR mix time
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam6  : C soft 90 purge pulse (e.g. eSNOB)
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) D1, DP, CDC, Cpre, purge
