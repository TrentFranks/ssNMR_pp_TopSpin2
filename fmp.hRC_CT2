;hRX_XT2

;###################################################################
;#    -X (13C) Hard-Pulse Spin Echo *T2* is ~0.6*Signal(t0)        #
;# Hard Pulse does ~NOT~ compensate for scalar coupling evolution  #
;# Soft Pulse DOES compensate for scalar coupling evolution if set #
;#     Use appropriate (vd<10ms) vdlist with Topspin Analysis      #
;#        Analysis -> T1/T2 Relaxation Analysis -> uxnmrt2         #
;###################################################################

;Cross Polarization for X channel (13C)
;Measurement of C T2 with vd list by simple spin echo
;written by WTF
;Avance II+ version

;$COMMENT= H-X CP with X T2 simple spin echo
;$CLASS=Solids
;$DIM=2D
;$TYPE=CPMAS
;$SUBTYPE=Relaxation
;$OWNER=guest
;prosol relations=<solids_cp>

define delay ONTIME
;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

                            ;##########################
;$EXTERN                    ;# Python insertion point #
                            ;##########################

Prepare, ze

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"d6=vd"
"ONTIME=aq+p10+2*d6"

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

"d6=vd"
"ONTIME=aq+p10+2*d6"

#include <Standard_Prot.incl>
        ;acq, CP time, etc.
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

                         (p3  pl2  ph1):H
  ;(p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H
  (p10 pl10 ph2):C  (p10:spf0 pl20 ph0):H
  0.3u cpds2:H ;decouple 1H during acquisition

;######################################################
;#            Hard or Selective Pulse                 #
;######################################################

  vd               
#ifdef SP
  (p8:spf8 pl28 ph3):C 
#else
  (p2 pl1 ph3):C 
#endif
  vd

  0.3u

;#######################################################
;#                     Acquisition                     #
;#######################################################

  go=Start ph31         
  1m do:H 
  d1 mc #0 to Start F1QF(ivd) 

HaltAcqu, 1m
exit


ph0 = 0                ; H CP Spin lock
ph1 = 1 3              ; H Hard Pulse
ph2 = 0 0 2 2 1 1 3 3  ; X CP Spin Lock 
ph3 = 0 1 2 3 

ph31= 0 0 2 2 3 3 1 1  ; receiver

;#######################################################s

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C soft 180 (spnam8, pl28) use -DSP
;p10     : H-C CP at pl10 (C) and sp0 (H)
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl10    : X CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl20    : H CP pwr
;pl28    : C soft 180 
;d1      : recycle delay
;d6      : Echo delay -Decho 
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;spnam8  : soft X 180 e.g. rSnob
;zgoptns : (-D) SP