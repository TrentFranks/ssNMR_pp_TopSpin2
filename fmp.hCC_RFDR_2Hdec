;CC with ZQ RFDR mixing (and PDSD/DARR) (TopSpin 2.3)

;######################################################
;#                                                    #
;# RFDR                                               #
;#   A.E. Bennett, J.H. OK, S.. Vega, R.G. Griffin    #
;#      Journ. Chem. Phys. 96 8624                    #
;#                                                    #
;# Apply one pi-pulse on X(Carbon) each rotor period  #
;#   d8 (DARR) period after RFDR                      #
;#                                                    #
;#  ns = n*4 (full cycle =16)                         #
;#  l3 >= n*4                                         #
;#                                                    #
;#  Indirect Dimension phasing is 90,-180             #
;#                                                    #
;######################################################
;
; Options D1, DP, RFDRcmpd, Cpre
; written Barth van Rossum, W Trent Franks
; FMP Berlin

;$COMMENT= HC CP with C Z-mixing
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=ZQ mixing
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <CHD_defs.incl>

;######################################################
;#                   Define Mixing                    #
;#            RFDR w/ simple 180 is Default           #
;######################################################

#include <fmp.RFDR_2H.subr>

;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0/2-4.0u-p1*2/3.14159";#   t1_init => 90,-180   #
"in30=inf1"                 ;#                        #
define loopcounter go_t1    ;#   No increment if 1D   #
"go_t1=td1"                 ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;##########################
                            
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################
Prepare, ze

"d30=d30/2"                 ;#divide by 2 for mc macro#

"ONTIME=aq+d0+d30+p10+p8+l3*TauR" ;#  DARR left out   #

;######################################################
;#      Long ACQ  Protections: Pre-Check              #
;######################################################

#ifdef lacq

#include <LACQ_Prot.incl>
#include <LACQ_ONTIME_prot.incl>

#endif

#ifndef lacq

#include <Standard_Prot.incl>
#include <ONTIME_prot.incl>

#endif

;######################################################
;#                   Other Protections                #
;######################################################

#include <RFDR_prot.incl>
            ;d8 less than 1s 
#include <DARR_prot.incl>
            ;loop l3 < 100; RFDRmix < 10ms
#include <t1_prot.incl>
        ;check t1 evolution time

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H do:D

1u  pl22:D

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row                            
#include <t1evol_prot.incl> 
                            
#ifdef Cpre
 ; Carbon Presatuaration
 (p8 pl8 ph20):C
#endif

;######################################################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C
#else
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
#endif

if "go_t1>1"
{                           ;##########################
  d0 cpds2:H cpds3:D        ;#      t1 evolution      #
  0.3u do:H do:D            ;##########################
}

;######################################################
;#              Homonuclear RFDR Mixing               #
;######################################################

  (p1 pl1 ph3):C   (p1 pl2):H  ;store
  
#ifdef cmpd
 subr RFDR_cmpd()
#else
 subr RFDR_()             ; DEFAULT TO RFDR w/ PDSD
#endif

1u  pl22:D
  
  (p1 pl1 ph4):C (p1 pl2):H ;readout
    
  0.3u cpds2:H cpds3:D

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 		;start ADC with ph31 signal routing

#ifdef CDC
  d30                   ; Constant duty cycle
#endif

1m do:H do:D            ;Decoupler off
lo to Start times ns

30m mc #0 to Start F1PH(dp3,id0 & dd30)

HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=16)            #
;#######################################################

ph0=  0
ph1=  1 3
ph2=  {0}*8 {2}*8
ph3=  1 1 3 3  
ph4=  1 1 2 2 3 3 0 0  

ph10= {1 3}*4 {3 1}*4  ; X90 DP

ph20= 0 2

;#######################################################
;#                    RFDR Phases                      #
;#######################################################

#ifdef xy16
ph23= 0 1 0 1 1 0 1 0 2 3 2 3 3 2 3 2
ph24= 0 1 0 1 1 0 1 0 2 3 2 3 3 2 3 2
#else
ph23= 0 1 0 1 1 0 1 0
ph24= 1 2 1 2 2 1 2 1 
#endif

ph31= 2 0 1 3 0 2 3 1 0 2 3 1 2 0 1 3

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C Presat (-DCpre)
;p10     : HC CP contact pl10 (C) and sp0 (H)
;sp0     : H CP pwr
;pl1     : C HP pwr
;pl2     : H HP pwr
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (CW during RFDR)
;pl16    : H DARR pwr (DARR = MAS freq; PDSD =120)
;l3      : RFDR repetitions
;d1      : recycle delay
;d8      : PDSD/DARR mix time
;RFDRmix : mix time RFDR
;pcpd2   : dec pulse
;pcpd3   : RFDR dec pulse 
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;cpdprg3 : RFDR Decoupling cw, tppm (at pl13)
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) cmpd, D1, DP, CDC, Cpre, xy16
