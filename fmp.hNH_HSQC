;HN Proton-Detect INEPT x-fer experiment (TopSpin 2.3)

;######################################################
;#                                                    #
;#  HN HSQC for solids                                #
;#                                                    #
;######################################################
;
;$COMMENT= HN INEPT Transfer Experiment
;$CLASS=Solids
;$DIM=2D
;$TYPE= 1H detect Hetero
;$SUBTYPE=Scalar Transfer
;$OWNER=guest
;
#include <HCN_defs.incl>

;######################################################
;#          Define Scalar Mixing Parameters           #
;######################################################

define delay TauHN
"TauHN=d26"

;######################################################
;#                Evolution Parameters                #
;######################################################

"in0=inf1"                  ;##########################
"d0=3u"                     ;#     t1_init => 0,0     #
"in2=inf1/2"                ;##########################

define loopcounter go_t1
"go_t1=td1"

define loopcounter T1evo 
"T1evo=larger(td1,cnst30)"

"d2=TauR+(T1evo*in2)"

define delay ONTIME
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################

Prepare, ze

"d2=d2/2"                 ;# divide by 2 for mc macro #

;######################################################
;#               Protections: Pre-Check               #
;######################################################

"ONTIME=aq+4*p3+2*p4+p5+5*p21+4*p22+4*p1+2*p2+d18"

#include <1H_ON_prot.incl>
        ;total RF depsosition restriction
; Protections for Proton Presat, H Acq, etc

Start, d1 do:N

;######################
;#      PRESAT        #
;######################

#ifdef Pre
  3u pl18:H
  d18 cw:H ph20
  d13 do:H
#endif

;#####################
;#     HN INEPT      #
;#####################

  (p3 pl2 ph1):H
  TauHN
  (center (p4 pl2 ph1):H (p22 pl21 ph2):N )
  TauHN

;#############
;# 1H Purge  #
;#############

#ifdef PP
  (p5 ph1):H
#endif

;##############
;# Store IzSz #
;##############

  (p3 pl2 ph3):H

;##########################
;# Time evolution on IzSx #
;##########################

  d2
  (p21 pl21 ph4):N

if "go_t1>1"
{
  (center
    (d0):N
    (p3 ph20 p4 ph21 p3 ph20):H       ; Composite pulses on H and C
    (p1 pl1 ph20 p2 ph21 p1 ph20):C 
  )
}

if "go_t1<=1"
{
    (p3 ph20 p4 ph21 p3 ph20):H       ; Composite pulses on H and C
}

  (p21 ph5):N 	 ;15N 90 pulse 
  d2

;####################
;# INEPT Back to 1H #
;####################

  (p3 pl2 ph6):H
  TauHN
  (center 
    (p4 pl2 ph1):H 
    (p22 pl21 ph7):N
  )
  TauHN

  0.5u pl29:N     ; GARP power on 15N
  1u  cpd3:N

  go=Start ph31
  1m do:N
  30m mc #0 to Start
      F1PH(ip4, id0 & dd2)

HaltAcqu, 1m
exit

;#####################
;#  Phase Cycle, n>4 #
;#####################

ph1= 0
ph2= {0}*8 {2}*8
ph3= 1
ph4= 0 2
ph5= 0 0 2 2
ph6= {0}*4 {2}*4
ph7= {0}*8 {2}*8

ph20= 0
ph21= 1
ph22= 2

ph31= 2 0 0 2   0 2 2 0

;######################################################################

;PARAMETERS:
;p1      : C 90 at pl1
;p2      : C 180 at pl1
;p3      : H 90 at pl2
;p4      : H 180 at pl2
;p5      : H spinlock purge
;p21     : N 90 pl21
;p22     : N 180 pl21
;d0      : initial t1 (3u)
;d1      : recycle delay
;d2      : total t1 ev time
;d26     : HN INEPT time (1/4J ~ 2.3ms)
;NS      : 4 * n
;DS      : >= 4
;pl1     : C HP pwr level
;pl2     : H HP pwr level
;pl21    : N HP pwr level
;cpds3   : N dec cpdprg3
;zgoptns : (-D) Pre, PP
