; Transverse SPC5 subroutines
; written by W. Trent Franks Feb. 2010
; FMP Berlin

; Contents:
;  SPC5_2  (Broadband)
;  M. Hohwy, C.M. Rienstra, C.P. Jaroniec, and R.G. Griffin
;  J. Chem. Phys. 1999,110,7983-7992
;  SPC5_3  (Narrowband)
;  M. Hohwy, C. M. Rienstra, R. G. Griffin
;  J. Chem. Phys. 2002, 117 4973-4987
;
;#########################
;#  INCLUDE THESE PHASES #
;#########################
;
;SPC5
;ph15= (360) 90 162 234 306 18 270 342 54 126 198
;ph16= (360) 270 342 54 126 198 90 162 234 306 18
;ph25= (360) 0 72 144 216 288 180 252 324 36 108
;ph26= (360) 180 252 324 36 108 0 72 144 216 288
;
;ph29= (360) 90 162 234 306 18 270 342 54 126 198
;ph30= (360) 270 342 54 126 198 90 162 234 306 18
;
;#############################
;#  INCLUDE THESE PARAMETERS #
;#############################
;
;l15   	: C elements (x5; x10 for full CSA compensation)  ;
;pl25   : C5 X ampl (10wr/3 (SPC5_3) or 5wr (SPC5_2)
;cnst31 : MAS Frequency 
;
;###############################################
;#   Calculations performed in fmp.SPC5.subr   #
;###############################################

   "SPC52mix2=(10*C52p360)*l15"   ;SPC5 mixing time
   "SPC53mix2=(10*C53p360)*l15"   ;SPC5 mixing time

;###################
;# Building blocks #
;#      SPC5 2     #
;###################

subroutine SPC5_2exc2()
{
  1u rpp25
  1u rpp26
  (TauRm4 pl16):H 
  1u pl25:C  pl13:H 
  1u cw:H  ;

if (l5 >= 1)
{
C522,
    C52p90:C   ph25 ipp29 ipp30        
    C52p360:C  ph26^
    C52p270:C  ph25^
lo to C522 times l5   ;Number of C5's
}
}

subroutine SPC5_2rec2()
{

if (l5 >= 1)
{
C52r2,
    C52p90:C   ph29         
    C52p360:C  ph30^
    C52p270:C  ph29^
lo to C52r2 times l5   ;Number of C5's
}
  2u do:H   
  TauRm2
}

subroutine SPC5_2rec_dq2()
{

if (l5 >= 1)
{
C52r2,
    C52p90:C   ph29         
    C52p360:C  ph30^
    C52p270:C  ph29^
lo to C52r2 times l5   ;Number of C5's
}
  2u do:H   
  TauRm2
}

;###################
;# Building blocks #
;#      SPC5 3     #
;###################


subroutine SPC5_3exc2()
{
  1u rpp25
  1u rpp26
  (TauRm4 pl16):H 
  1u pl15:C  pl13:H 
  1u cw:H  ;

if (l5 >= 1)
{
C532,
    C53p90:C   ph25 ipp29 ipp30        
    C53p360:C  ph26^
    C53p270:C  ph25^
lo to C532 times l5   ;Number of C5's
}
}

subroutine SPC5_3rec2()
{

if (l5 >= 1)
{
C53r2,
    C53p90:C   ph25         
    C53p360:C  ph26^
    C53p270:C  ph25^
lo to C53r2 times l5   ;Number of C5's
}
  2u do:H   
  TauRm2
}

subroutine SPC5_3rec_dq2()
{

if (l5 >= 1)
{
C53r2,
    C53p90:C   ph29         
    C53p360:C  ph30^
    C53p270:C  ph29^
lo to C53r2 times l5   ;Number of C5's
}
  2u do:H   
  TauRm4
}
