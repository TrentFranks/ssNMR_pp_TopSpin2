;CC DQ-SQ INADEQUATE experiment with SPC5 (TopSpin 2.3)

;######################################################
;#                                                    #
;# SPC5(_2,_3)  INADEQUATE (DQ-SQ) experiment         #
;#  SPC5_2  (Broadband)                               #
;#   M. Hohwy, C.M. Rienstra, C.P. Jaroniec, and      #
;#     R.G. Griffin J. Chem. Phys. 1999,110,7983-7992 #
;# SPC5_3  (Narrowband)                               #
;#   M. Hohwy, C. M. Rienstra, R. G. Griffin          #
;#     J. Chem. Phys. 2002, 117 4973-4987             #
;#                                                    #
;# Adjust for MAXIMUM                                 #
;#  pl15=10/3*wr (Default,SPC5_3) or 5*wr (SPC5_2)    #
;#  l5 is a multiple of 5                             #
;#                                                    #
;#  pl15(SPC5_3)= 10/3*wr; l5 >= n*10 ; ns = n*4      #
;#      SWH(F1)= 5*n*wr/3                             #
;#      IN_F(F1)=  3/5*n*wr n=1,2,3...                #
;#                                                    #
;#  pl15(SPC5_2)= 5*wr; l5 >= n*10 ; ns = n*4         #
;#      SWH(F1)=   5*n*wr/2                           #
;#      IN_F(F1)=  2/5*n*wr n=1,2,3...                #
;#                                                    #
;#      Indirect Dimension phasing is 0,0             #
;#                                                    #
;######################################################
;
; Options DP, SPC52, Cpre, CDC
; written Barth van Rossum and W. Trent Franks

;$COMMENT= HC CP with CC DQ evolution and SQ acquisition
;$CLASS=Solids
;$DIM=2D
;$TYPE=Homonuclear
;$SUBTYPE=INADEQUATE
;$OWNER=guest

;prosol relations=<solids_cp>

;#include <Avancesolids.incl>
#include <C_detect_def.incl>

;######################################################
;#                   Define Mixing                    #
;#        SPC5_3 is Default, SPC5_2 with -DSPC52      #
;######################################################

#include <fmp.SPC5.subr>

;cnst11 : adjust t=0 for acq, if digmod = baseopt
"acqt0=1u*cnst11"

"in0=inf1"                  ;##########################
"d0=in0-1.2u"               ;#     t1_init -> 0,0     #
"in30=inf1"                 ;#                        #
define loopcounter go_t1    ;#   No increment if 1D   #
"go_t1=td1"                 ;#                        #
"l10=1"                     ;##########################

define delay ONTIME         ;##########################
define loopcounter T1evo    ;# Power Deposition Calcs #
"T1evo=larger(td1,cnst30)"  ;#   Constant Duty Cycle  #
"d30=T1evo*in30"            ;##########################
                            
                            ;##########################
;$EXTERN                    ;# python insertion point #
                            ;##########################
Prepare, ze

"d30=d30/2"                 ;# divide by 2 for mc macro #
#ifdef SPC52
  "ONTIME=aq+d0+d30+p10+p8+2*SPC52mix"
#else
  "ONTIME=aq+d0+d30+p10+p8+2*SPC53mix"
#endif

;######################################################
;#               Protections: Pre-Check               #
;######################################################

#include <Standard_Prot.incl>
        ;acquisition, CP time, PDSD/DARR, and echo restrictions
#include <SPC5_prot.incl>
        ;loop l5 
#include <t1_prot.incl>
        ;check t1 evolution time
#include <ONTIME_prot.incl>
        ;total RF depsosition restriction

;######################################################
;#           Start of Active Pulse Program            #
;######################################################

Start, d1 do:H

  1m rpp15                  ;##########################  
  1m rpp16                  ;#  C5 phases reset here  #
  1m rpp25                  ;#    Filter + DQ evol    #
  1m rpp26                  ;##########################

;######################################################
;#               Protections: In Situ                 #
;######################################################

; d0,d30 checked every row
#include <t1evol_prot.incl>

#ifdef CPRE
 ; Carbon Presatuaration
 (p0 pl0 ph20):C
#endif

;######################################################
;#              Cross/Direct Polarization             #
;######################################################

#ifdef DP
   (p1 pl1 ph10):C
#else
                         (p3  pl2  ph1):H
  (p10:spf0 pl10 ph2):C  (p10 pl20 ph0):H 
#endif

;######################################################
;#         Homonuclear SPC5_(3,2) Excitation          #
;##########################!###########################
  
  (p1 pl1 ph3):C   (p1 pl2):H  ;store

#ifdef SPC52
  subr SPC5_2exc()
#else
  subr SPC5_3exc()
#endif

;######################################################
;#                  DQ-t1 evolution                   #
;# t=0 for first point subsequent points set in situ  #
;######################################################

if "l10==2"  
    {
    "d0=in0-1.2u"
    }
if "l10 > 1"
  {
  0.3u  do:H
  d0    cpds2:H 
  0.3u  do:H
  0.3u  pl13:H pl17:C
  0.3u  cw:H 
  }

;######################################################
;#          Homonuclear SPC5_(3,2) Recovery           #
;##########################!###########################
  
#ifdef SPC52
  subr SPC5_2rec_dq()
#else
  subr SPC5_3rec_dq()
#endif
  
  (p1 pl1 ph4):C   (p1 pl2):H  ;readout
    
  0.3u cpds2:H 

;#######################################################
;#                     Acquisition                     #
;#######################################################

gosc ph31 	; Start ADC with ph31 signal routing

#ifdef CDC
  d30           ; Constant duty cycle
#endif

1m do:H         ; Decoupler off

1m ip15*90
1m ip16*90              ; DQF, add 2 to rec phase every scan

lo to Start times ns

d1 mc #0 to Start F1PH(ip25*45 & ip26*45,id0 & dd30 & iu10) 

HaltAcqu, 1m
exit

;#######################################################
;#             Phase Cycle (min=4, full=16)            #
;#######################################################

ph0=  0
ph1=  1 3
ph2=  {0}*8 {2}*8
ph3=  1 1 3 3 
ph4=  1 1 2 2 3 3 0 0  

ph10= 1 3 3 1 2 0 0 2  ; X90 DP

ph20= 0 2

ph31= 0 2 2 0  2 0 0 2   2 0 0 2  0 2 2 0 
      1 3 3 1  3 1 1 3   3 1 1 3  1 3 3 1
      
;#######################################################
;#                    SPC5 Phases                      #
;#######################################################

ph15= (360) 90 162 234 306 18 270 342 54 126 198
ph16= (360) 270 342 54 126 198 90 162 234 306 18
ph25= (360) 0 72 144 216 288 180 252 324 36 108
ph26= (360) 180 252 324 36 108 0 72 144 216 288

;PARAMETERS:
;p1      : C 90  (pl1)
;p2      : C 180 (pl1)
;p3      : H 90  (pl2)
;p8      : C Presat (-DCpre)
;p10     : HC CP contact pl10 (C) and sp0 (H)

;pl1     : C HP pwr
;pl2     : H HP pwr
;pl10    : C CP pwr
;pl12    : H dec pwr
;pl13    : H alt dec pwr (Spinlock and CW)
;pl15    : C SPC5 pwr (10/3wr if SPC53; 5wr if SPC52)
;pl20    : H CP pwr
;l5      : SPC5 repetitions (doubled)

;d1      : recycle delay
;SPC52mix: SPC52 mix time (half)
;SPC53mix: SPC53 mix time (half)
;pcpd2   : pulse length in decoupling sequence
;cpdprg2 : cw, tppm (at pl12), or lgs, cwlg. cwlgs (LG-decoupling 
;spnam0  : use e.g. ramp.100 for variable amplitude CP
;cnst30  : expected td1 for use in constant Duty (-DCDC)
;cnst31  : MAS Rate
;FnMode  : TPPI, States or States-TPPI
;zgoptns : (-D) DP, CDC, CPRE, SPC52
